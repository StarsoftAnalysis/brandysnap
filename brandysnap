#!/usr/bin/perl

# Forked from 0.2.5 to hack on Windows -- commented as 'W7'
# To do:
#   * can we do hard links!?    yes   
#   * lots of stuff with slashes   not needed
#   * different defaults?
#   * is blocksize 512?
#   * absPath()
#   * sourceDir()
#   * C:\ looks remote to rsync -- might require cygwin
#   * tests for remoteness
#   * absPath is OK, but not for things that are for rsync -- maybe just don't
#      absPath them, make the user specify them properly
#   * so need to tweak paths when passing them to rsync
#      in rsyncReadable and other places
#   * Signals!!! -- ctrl-c works with cygwin!
#   * check if /cygdrive is really that, or has been set to something else.

# Assumptions / limitations
#   * assumes the usual /cygdrive/c structure
#
# Problems -- permissions / ACLs on /cygdrive/c -- on w7u it's
#   Chris@W7U /cygdrive
#   $ ls -la
#   total 12
#   dr-xr-xr-x  1 Chris            None             0 Nov  4 22:16 .
#   drwxr-xr-x+ 1 Chris            None             0 Nov  4 17:05 ..
#   d---------+ 1 TrustedInstaller TrustedInstaller 0 Nov  3 23:32 c
#   drwxr-xr-x  1 Chris            None             0 Oct 18 15:44 d
# - can create backup, but not delete stuff
#   - better without --relative??
# - maybe we just can't create the backup within /cygdrive/c, 
#   which is a stupid place to put it anyway.
# Some suggest putting noacl in fstab, but that means everything
# gets owned by the brandysnap runner.
# Plan: treat /cygdrive/c as a special case -- it gets 000 permissions
# so set them to 777.
#   -- per dest, if dest include /cygdrive/c
# 
#########################################################################
# brandysnap: rsync-based backup snapshot creation and management       #
# Copyright (C) 2011,2012 Chris Dennis, chris@starsoftanalysis.co.uk    #
#                                                                       #
# This program is free software: you can redistribute it and/or modify  #
# it under the terms of the GNU General Public License as published by  #
# the Free Software Foundation, either version 3 of the License, or     #
# (at your option) any later version.                                   #
#                                                                       #
# This program is distributed in the hope that it will be useful,       #
# but WITHOUT ANY WARRANTY; without even the implied warranty of        #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the         #
# GNU General Public License for more details.                          #
#                                                                       #
# You should have received a copy of the GNU General Public License     #
# along with this program.  If not, see <http://www.gnu.org/licenses/>. #
#########################################################################

# Coding style:
# CONSTANTS in upper case
# Globals with a leading capital
# otherVariables in camelCase (except for some frequently used things like @S)
# Formatting this file -- see the 'vim:' line at the end,
# and limit lines to 100 characters if possible.
# Keep it neat and tidy!

use 5.10.1;         
use strict;
use warnings;

use Time::Local;
use POSIX qw(ceil floor strftime);
use Date::Manip;    # see http://search.cpan.org/~sbeck/Date-Manip-6.23/lib/Date/Manip/DM6.pod
use Getopt::Long qw(GetOptionsFromArray :config ignore_case auto_abbrev);
use Math::Combinatorics;
use JSON;           # for metadata file
use File::Path qw(remove_tree);
use File::Glob qw(:glob); 
use File::Find;
use File::Temp qw(tempfile tempdir);
use File::Spec::Functions qw(canonpath rel2abs splitdir catfile catdir);
use File::Basename;
use Fcntl qw(:flock);
use Scalar::Util qw(looks_like_number);
use IPC::Open3;
use Symbol qw(gensym);
use IO::File;
use Sys::Hostname;
use Carp;
use Data::Dumper;   # only for dev

# Operating system -- assume Linux unless these flags say otherwise ===============================

my $Win = 0;
my $Cygwin = 0;
print "OS=$^O\n";
given ($^O) {   # Don't forget: $^O is the OS Perl was compiled for, not what it's running on.
    when (/Win/) { $Win = 1; } # TODO if /Win/, look for cygwin environment
    when ('cygwin') { $Win = 1; $Cygwin = 1; }
    # TODO: if Win but not cygwin, could check for '-d /cygdrive' to see if we're in cygwin env.
}
my $Cygdrive  = '/cygdrive';    # default
my $CygdriveC = '/cygdrive/c';
if ($Cygwin) {
    chomp(my $cygpath = `cygpath --windir` // '');  # returns e.g. /cygdrive/c/Windows
    if (!$?) {
        ($CygdriveC = $cygpath)   =~ s|/[^/]*$||;    # gets e.g. /cygdrive/c
        ($Cygdrive  = $CygdriveC) =~ s|/[^/]*$||;    # gets e.g. /cygdrive
    }
}

# Interrupts and other housekeeping ===============================================================

# get a stacktrace: (may get turned off later)
$SIG{__DIE__} = sub { Carp::confess(@_) };

# Make this script robust against interruptions.
# $Interrupted becomes true if the script has been interrupted
my $Interrupted = '';
# Set the process group so that we can tidy up child processes:
# What's the W7 way for this?: setpgrp(0, 0);
# Trap interrupts: set a flag and make sure any children are killed
sub killgroup {
    my $sig = shift;
    $SIG{$sig} = 'IGNORE'; # don't kill ourselves again
    $Interrupted = $sig; 
    kill($sig, -$$);
};
local $SIG{HUP}  = \&killgroup;
local $SIG{INT}  = \&killgroup;
local $SIG{QUIT} = \&killgroup;
local $SIG{ABRT} = \&killgroup;
local $SIG{TERM} = \&killgroup;

# Turn off buffering on STDOUT to get full error messages
$| = 1;

# Globals and constants ============================================================================

my $Script = 'brandysnap';

# Version of this script
our $VERSION = '0.2.7portable';

# A copy of @ARG before it gets shifted
my @ARGVcopy = @ARGV;

# Exit codes
use constant EXITOK     => 0;
use constant EXITCONFIG => 1;   # Configuration/option error
use constant EXITSPECS  => 2;   # Spec error
use constant EXITRSYNC  => 3;   # Error running rsync
use constant EXITINT    => 4;   # Interrupted
use constant EXITNOLOCK => 5;   # Can't get lock
my $ExitCode = EXITOK;

# Options -- need some options to start with
our %Opt = ();   
$Opt{debug}    = {'ap'=>1}; #{'rc'=>1, 'ap'=>1, 'po'=>1 }; # Set this when debugging processOptions(), or just {}
$Opt{verbose}  = 1;
$Opt{loglevel} = 1;

# Template for snapshot directory names -- the date part:
# (if you change this, existing snapshots will not be found!)
use constant STAMPFMT  => "%Y%m%d-%H%M%S";
use constant STAMPFMT0 => '00000000-000000';

# Hash of snapshots for the current destination, keyed by snapshot Unix time,
# each entry containing these fields:
#  name     - name of the snapshot directory e.g. 'docs-20111130-320234'
#  path     - directory path to name (NOT ending with '/')
#  fullname - same as path/name   
#  unfinished - true if it's an unfinished snapshot
#  size     - size of the snapshot in bytes
#  remote   - true if it's on a remote destination
#  daemon   - true if it's remote and accessed via an rsync daemon
my %S;

# Date/time constants and globals 

# A value that will be bigger than any value of 'time' or any total of lots of 'time's
use constant MAXTIME => 2**53;  # according to http://www.perlmonks.org/?node_id=718414

# An 'unlimited' number of snapshots
use constant UNLIMITED => 99999;

# Days in the month (ignoring leap years)
my @DiM = (31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31);

# Datecalc deltas for each period:
my @PrevP = (
    '-0:0:-0:0:1:0:0',  # h 
    '-0:0:-0:1:0:0:0',  # d
    '-0:0:-1:0:0:0:0',  # w
    '-0:1:-0:0:0:0:0',  # m
    '-1:0:-0:0:0:0:0',  # y
);

# Date::Manip's period identifiers
# (see http://search.cpan.org/~sbeck/Date-Manip-6.24/lib/Date/Manip/Delta.pod#PRINTF_DIRECTIVES)# 
# indexed by periodn
my @DMperiod = ('h', 'd', 'w', 'M', 'y');

# For converting e.g. 'd' to 1
my %Level = (h => 0, d => 1, w => 2, m => 3, M => 3, y => 4);

my @LevelName = ('hour', 'day', 'week', 'month', 'year');
my %LevelName = ('h' => 'hour',
                 'd' => 'day',
                 'w' => 'week',
                 'm' => 'month',
                 'M' => 'month',
                 'y' => 'year',
                 ''  => '');    # for 'old' cSpecs

# Number of hours in periods at each level
# (approximate for months and years)
my @Hours = (1, 24, 24*7, 24*7*4, 24*7*4*12);   

# Periods in seconds:
use constant MINUTE => 60;
use constant HOUR   => MINUTE * 60;
use constant DAY    => HOUR * 24;
use constant WEEK   => DAY * 7;
# months and years aren't fixed in terms of seconds!

# Regexp used for parsing a spec, e.g. 1d or 1d7 or 0-2d31
my $specRegex = 
           '\s*(?<freqmin>\d+)
            \s*(-\s*(?<freqmax>\d+))?
            \s*(?<period>[hdwmy])
            \s*(?<count>\d*)
            \s*';

# An empty temporary directory for use in deleteSnapshot(), rsyncMkdir(), etc.
# (yes, I know, this is a bit of a kludge)
my $EmptyDir = tempdir(CLEANUP => 1);

# Summary of results for each destination/source
# (initialised in processOptions())
my %Summary;
# name of placeholder indicating result of whole destination
# - shouldn't be a possible/likely file/dir name
my $DestResult = '<bs-dest-result>';

# Arrays and hashes defining types of options
# All options
my @AllOpt = qw(
    debug verbose verbosity loglevel test
    hbest dbest wbest mbest ybest
    safe calendar strict
    config destination spec template source
    snapshot delete delete-cp
    dry-run version help
    weekstart
    logfile lockfile 
    rsync-cmd rsync-opts remote-rsync-cmd
    include include-from exclude exclude-from
    expire-old
    bwlimit-in bwlimit-out
    status compress restart
    allow-restart ldcount
    all-failed some-failed
    min-interval
    );
my %AllOpt = (map {$_ => 1} @AllOpt);
# Required options (not including those that have a default value)
# also not including DestReqOpt ones
my @ReqOpt = qw(destination);
my %ReqOpt = (map {$_ => 1} @ReqOpt);
# Multi-value options (are also all string options)
my %MultiOpt = (map {$_ => 1} qw(source include include-from
    exclude exclude-from destination debug));
# Boolean options
my @BoolOpt  = qw(safe calendar snapshot delete delete-cp dry-run help strict 
    version expire-old status compress restart allow-restart);
my %BoolOpt = (map {$_ => 1} @BoolOpt);
# String options (NOT the multi-value ones, which are dealt with separately)
my @StringOpt = qw(config lockfile logfile template
    rsync-cmd rsync-opts remote-rsync-cmd
    all-failed some-failed);
my %StringOpt = (map {$_ => 1} @StringOpt);
# Numeric options
my @NumericOpt = qw(verbose loglevel test
    hbest dbest wbest mbest ybest 
    weekstart bwlimit-in bwlimit-out 
    ldcount min-interval);
my %NumericOpt = (map {$_ => 1} @NumericOpt);
# Level options (take values 0..9)
my @LevelOpt = qw(verbose loglevel);
my %LevelOpt = (map {$_ => 1} @LevelOpt);

# Contexts
# --------

# Valid contexts and nesting thereof:
my %ValidContext = (general=>1, destination=>1, source=>1);
my %AllowedContext = (
    general     => {source=>1, destination=>1},
    destination => {source=>1},
    source      => {},
);
# Matrix defining which contexts each option is allowed in
# (no spec within destination -- spec is only parsed once)
my %Matrix = (
	general => {map {$_ => 1} @AllOpt},      # all allowed
	destination => {map {$_ => 1} qw(
        hbest dbest wbest mbest ybest
        safe calendar strict
        source template spec
        include include-from exclude exclude-from   
        snapshot delete delete-cp
        weekstart
        rsync-cmd rsync-opts remote-rsync-cmd
        expire-old
        bwlimit-in bwlimit-out
        compress restart
        allow-restart ldcount
        all-failed some-failed
        min-interval
        verbose loglevel debug
    )},
    # some dest-related options can be specified per-source
    # so that each dest-source combination can be different
	source => {map {$_ => 1} qw(
        rsync-cmd rsync-opts remote-rsync-cmd
        include include-from exclude exclude-from
        bwlimit-in bwlimit-out
        compress restart
        allow-restart 
        min-interval
        verbose loglevel debug
    )},
);
# Required options for each destination
# (not including those that have a default value)
my @DestReqOpt = qw(source template spec);
my %DestReqOpt = (map {$_ => 1} @ReqOpt);

# Mainline code is at the end of this file

#===================================================================================================
# Logging

sub underline ($$) {
    # Underline a string with the given character
    my $string = shift;
    my $char   = shift;
    (my $lnl, $string, my $tnl) = ($string =~ /^(\n*)(.*)(\n*)$/);
    return $lnl . $string . "\n" . $char x length($string) . $tnl;
}

# --------------------------------------------------------------------------------------------------

sub under1 ($) {
    return underline(shift, '-');
}

# --------------------------------------------------------------------------------------------------

sub under2 ($) {
    return underline(shift, '=');
}

# --------------------------------------------------------------------------------------------------

sub chomps ($) {
    # A bit like chomp, but removes all new-lines from beginning of the line
    # and returns the number removed.
    my $textR = shift;
    $$textR =~ s/(\n*)$//;
    my $count = length($1) // 0;
    return $count;
}

# --------------------------------------------------------------------------------------------------

sub prechomps ($) {
    # A bit like chomp, but removes all new-lines from beginning of the line
    # and returns the number removed.
    my $textR = shift;
    $$textR =~ s/^(\n*)//;
    my $count = length($1) // 0;
    return $count;
}

# --------------------------------------------------------------------------------------------------

sub printit ($;$) {
    # Print depending on the level of --verbose
    # (trailing newline to be supplied)
    state $blanks = 0;  # keep track of blank lines
    my $text  = shift;
    my $level = shift // 1;
    return if $level > $Opt{verbose};
    my $newblanks = max(prechomps(\$text) - $blanks, 0);
    print "\n"x$newblanks . $text;
    $blanks = chomps(\$text) - 1;
}

# --------------------------------------------------------------------------------------------------

sub logit ($;$) {
    # Simple logging to a text file, preserving newlines in the text while adding a timestamp
    state $blanks = 0;  # keep track of blank lines
    return if !defined $Opt{validLogfile};
    my $text  = shift;
    my $level = shift // 1;
    return if $level > $Opt{loglevel};
    my $lnl = max(prechomps(\$text) - $blanks, 0);
    my $tnl = chomps(\$text);
    $text =~ s/\n/\n                  /gs;
    my $now  = strftime "%y/%m/%d %H:%M:%S ", localtime;
#   my $oldfh = select LOGFILE;
#   $| = 1;
#   open  LOGFILE,'>>', "$Opt{validLogfile}" or die "Unable to write to logfile $Opt{validLogfile}";
    print LOGFILE "\n"x$lnl . "${now}${text}" . "\n"x$tnl;
#   close LOGFILE;
#   select $oldfh;
    $blanks = $tnl - 1;
}

# --------------------------------------------------------------------------------------------------

sub printlog ($;$$) {
    # Print and log depending on the level of verbose
    my $text   = shift;
    my $plevel = shift // 1;
    my $llevel = shift // $plevel;
    printit $text, $plevel;
    logit $text, $llevel;
}

# --------------------------------------------------------------------------------------------------

sub debug ($;$) {
    # Print debugging information to STDOUT and log
    # depending on the type of debugging
    my $text  = shift;
    my $level = shift // '';
    my $prefix = '';
    $prefix = "$level: " if $level;
    if ($Opt{debug}->{lc($level)}) {
        (my $leadingnl, $text) = ($text =~ m/^(\n*)(.*)$/s); 
        printlog "${leadingnl}dbg: $prefix$text", 0,0;
    }
}

#===================================================================================================

# Useful stuff

# Simple min/max routines for use with numbers
sub min ($$) {
    my $a = shift;
    my $b = shift;
    return ($a < $b) ? $a : $b;
}
sub max ($$) {
    my $a = shift;
    my $b = shift;
    return ($a > $b) ? $a : $b;
}

#---------------------------------------------------------------------------------------------------

my @Suffix = ('B', 'KiB', 'MiB', 'GiB', 'TiB', 'PiB', 'EiB', 'ZiB', 'YiB');
        # see http://physics.nist.gov/cuu/Units/binary.html
sub bytes ($) {
    # Simple routine for displaying a file size
    # Return '' if size is zero or less
    # (it could be -1 for a new snapshot during a dry-run)
    my $n = shift;
    return '' if $n <= 0;
    my $base = 1024;
    my $mult = 0;
    while ($n >= $base) { 
        $n /= $base;
        $mult += 1;
    }
    my $dp = 0;
    if ($mult > 0) {
        # minimal code for displaying 3 significant figures:
        if ($n >= 100) {
            $dp = 0;
        } elsif ($n >= 10) {
            $dp = 1;
        } else {
            $dp = 2;
        }
    }
    return sprintf ("%4.*f %s", $dp, $n, $Suffix[$mult]);
}

#---------------------------------------------------------------------------------------------------

sub superchomp ($) {
    # Chomp that also removes trailing \015 -- why is this needed??
    # - because in cygwin \n is just \012, but files (such as the
    # configuration file) have probably been edited with Windows' \015\012 newlines
    # That doesn't explain why this is only need for 'spec' though.
    foreach (@_) {
        s/\r?\n?$//;
    }
}

#---------------------------------------------------------------------------------------------------

sub combinations ($@) {
    # Return all ways of choosing $n items from @list
    my $n    = shift;  
    my @list = @_;
    my @combs;
    my $generator = Math::Combinatorics->new(count => $n, data => [@list],);
    while (my @combo = $generator->next_combination) {
        # (elements in @combo are in same order as in @list)
        push @combs, [@combo];
    }
    return @combs;
}

#---------------------------------------------------------------------------------------------------

sub stripQuotes ($) {
    # If the user has enclosed the option in single or double quotes,  
    # we'll strip them off (and add single quotes later).
    # If there are more than one set, e.g. '"string"' or "'string'",
    # assume that the string is really supposed to have the inner
    # set as part of its value.
    # Any other quotes, such as abc'def or xyz" will also be treated
    # as part of the value.
    # And double quotes will be escaped, ready for the string to
    # be wrapped in double quotes when used in the rsync command.
    # Also strips surrounding white-space, e.g. | str ing  | -> |str ing|
    my $string = shift;
    die "stripQuotes -- $string is a reference!!" if ref($string);
    my $temp = $string; # just for debugging
    $string =~ s/^\s+//;                # strip leading white-space
    $string =~ s/\s+$//;                # strip trailing white-space
    if (!($string =~ s/^'(.*)'$/$1/)) { # strip single quotes
        $string =~ s/^"(.*)"$/$1/;      # strip double quotes if no singles
    }
    $string =~ s/^\s+//;                # strip white-space again
    $string =~ s/\s+$//;
    $string =~ s/\"/\\\"/g;             # escape double quotes
    debug "|$temp| -> |$string|\n", 'sQ';
    return $string;
}

#---------------------------------------------------------------------------------------------------

sub dumpSnapshot ($) {
    # Return a printable version of a %S entry
    my $s = shift;
    return "$s (" . time2stamp($s) . ") => {name=>$S{$s}{name} path=>$S{$s}{path} " 
        . "fullname=>$S{$s}{fullname} unfinished=>$S{$s}{unfinished} size=>$S{$s}{size} "
        . "remote=>$S{$s}{remote} daemon=>$S{$s}{daemon}}";
}

#---------------------------------------------------------------------------------------------------

sub dumpSnapshots (@) {
    # Return a list of snapshots as a string for printing
    my @S = @_; 
    return join(', ', map {time2stamp($_)} @S);
}

#---------------------------------------------------------------------------------------------------

sub dumpSnapshots2 (\@) {
    # Return a list of snapshots as a string for printing
    # This version also uses %S to mark snapshots as good or bad
    my @S = @{(shift)};
    return join(', ', map {time2stamp($_) . ($S{$_}{unfinished} ? '-b' : '-g')} @S);
}

#---------------------------------------------------------------------------------------------------

sub rsort (@) {
    # Simple numeric reverse sort
    return sort {$b <=> $a} @_;
}

#---------------------------------------------------------------------------------------------------

sub fmtFreq ($$) {
    # Format the frequency as either a range or a single number
    my $freqmin = shift;
    my $freqmax = shift;
    if ($freqmin == $freqmax) {
        return "$freqmin";
    } else {
        return "$freqmin-$freqmax";
    }
}

#---------------------------------------------------------------------------------------------------

sub diskSpace ($) {
    # Convert a file size to the space used on the disk
    # by rounding it up to a multiple of the block size
    # NOTE: we assume a blocksize of 512 -- that's good enough, as this is only for displaying
    #       the approximate space used by a snapshot, and getting the real blocksize
    #       is probably impossible since we want to use rsync for everything.
    my $size = shift;
    my $blocksize = 512;
    return ceil($size / $blocksize) * $blocksize; 
}

# Date/time routines ===============================================================================

sub hms ($) {
    # Format a number of seconds as hh:mm:ss
    return strftime("\%H:\%M:\%S", gmtime(shift));
}

sub fmtHours ($) {
    # Pretty-print a time period given in hours
    my $hours = shift;
    my $delta = "0:0:0:0:$hours:0:0";
    debug "hours=$hours delta=$delta\n", 'fH';
    $delta = DateCalc($delta, "0:0:0:0:0:0:0", 1);  # normalise it
    my ($y, $M, $w, $d, $h, $m, $s) = split /:/, $delta;
    my @bits = split /:/, $delta;
    my $result = '';
    $result .= "$y years "   if $y != 0;
    $result .= "$M months "  if $M != 0; 
    $result .= "$w weeks "   if $w != 0; 
    $result .= "$d days "    if $d != 0; 
    $result .= "$h hours "   if $h != 0; 
    $result .= "$m minutes " if $m != 0; 
    $result .= "$s seconds " if $s != 0; 
    chop($result);  # trim final space    
    return $result;
}

#---------------------------------------------------------------------------------------------------

sub time2date ($) {
    # Convert a Unix timestamp to a Date::Manip date
    my $ts = shift;
    #my $date = new Date::Manip::Date;
    #my $err  = $date->parse("epoch $ts");
    #return $date;
    # this suddenly stopped working:
    return ParseDateString("epoch $ts");
}

#---------------------------------------------------------------------------------------------------

sub date2time ($) {
    # Convert a Date::Manip date to a Unix timestamp
    my $date = shift;
    return UnixDate($date, '%s');
}

#---------------------------------------------------------------------------------------------------

sub leapYear ($) {
    # Simple routine that assumes we're running sometime
    # between 2001 and 2099.
    # Returns 1 for a leap year, else 0
    my $moment = shift;
    my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) = localtime($moment);
    return (($year+1900 % 4) == 0);
}

#---------------------------------------------------------------------------------------------------

sub daysInYearMonth ($$) {
    # Return the number of days in the month specified by year and month number (0..11)
    my $y = shift;
    my $m = shift;
    my $days = $DiM[$m];
    $days += 1 if ($m == 1 and leapYear($y));
    return $days;
}

#---------------------------------------------------------------------------------------------------

sub daysInMonth ($) {
    # Return the number of days in the month in which 
    # the specified moment falls
    my $moment = shift;
    my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) = localtime($moment);
    return daysInYearMonth($year+1900, $mon);
}

#---------------------------------------------------------------------------------------------------

sub daysInYear ($) {
    # Return the number of days in the year 
    # in which the specified moment falls.
    my $moment = shift;
    return leapYear($moment) ? 366 : 365;
}

#---------------------------------------------------------------------------------------------------

sub aligned ($$) {
    # Return true if timesofar is a multiple
    # of the given period
    # (taking a month as 4 weeks)
    my $timesofar = shift;
    my $periodn   = shift;
    return ($timesofar % $Hours[$periodn]) == 0;
}

#---------------------------------------------------------------------------------------------------

# Timestamp format for snapshots is YYYYMMDD-hhmmss
# as defined by STAMPFMT and hard-coded in the following subroutine.

sub stamp2time ($) {
    # convert filename-type timestamp to Unix time
    my $stamp = shift;
    if ($stamp =~ /(?<Y>\d\d\d\d)(?<M>\d\d)(?<D>\d\d)-(?<h>\d\d)(?<m>\d\d)(?<s>\d\d)/) {
        return timelocal($+{s},$+{m},$+{h},$+{D},$+{M}-1,$+{Y});
    }
    debug "WARNING -- invalid stamp '$stamp' -- returning 0\n", 's2t';
    return 0;
}

#---------------------------------------------------------------------------------------------------

sub time2stamp ($) {
    # convert Unix time to filename timestamp
    my $time = shift;
    return STAMPFMT0 if !$time or $time == 0;
    my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) = localtime($time);
    return strftime(STAMPFMT, localtime($time));
}

#---------------------------------------------------------------------------------------------------

sub time2stampz ($) {
    # convert Unix time to filename timestamp, including timezone
    my $time = shift;
    return STAMPFMT0.'+0000' if !$time or $time == 0;
    my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) = localtime($time);
    return strftime(STAMPFMT.'%z', localtime($time));
}

#---------------------------------------------------------------------------------------------------

sub pair2range ($$) {
    # Convert a beginning/end pair of Unix times to a readable range
    my $beg = shift;
    my $end = shift;
    return time2stamp($beg) . '--' . time2stamp($end);
}

#---------------------------------------------------------------------------------------------------

sub fullName ($) {
    # Return the full version of an option name
    my $name = lc(shift);
    my @matches;
    foreach my $fullname (@AllOpt) {
        return $name if $name eq $fullname; # exact match
        push @matches, $fullname if $fullname =~ /^$name/i;
    }
    return $name if @matches == 0;
    return $matches[0] if @matches == 1;
    printlog "ERROR: option '$name' is ambiguous, matching any of " .
        join(', ', @matches) . "\n", 1,1;
    return '!*ambiguous';
}

#===================================================================================================
# Filing system stuff

sub remote ($) {
    # Returns 'true' if the path is considered by rsync to be remote
    # (via daemon or just ssh)
    # (See rsync's man page re the significance of ':')
    my $path = shift;
#    return 0 if ($Cygwin and $path =~ /^[A-Za-z]:(\\|\/)/);    # FIXME is this OK?
    return 1 if index($path, ':') > -1;
    return 0;
}

#---------------------------------------------------------------------------------------------------

sub remoteDaemon ($) {
    # Returns 'true' if the path is considered by rsync to be remote
    # via the rsync daemon
    # (See rsync's man page re the significance of '::' and 'rsync://')
    # W7 TODO what does this look like on Windows?
    my $path = shift;
    return 1 if $path =~ m|^rsync://|;
    return 1 if index($path, '::') > -1;
    return 0;
}

#---------------------------------------------------------------------------------------------------

sub anyDestRemoteDaemon () {
    # Returns 'true' if any of the destinations are using an rsync daemon
    foreach my $dest (@{$Opt{destination}}) {
        return 1 if remoteDaemon($dest);
    }
    return 0;
}

#---------------------------------------------------------------------------------------------------

sub absPath ($) {
    # Convert a file or directory name into an absolute
    # path, with '~' (home directory) expansion.
    my $path = shift;
    if ($Cygwin) {
#        # cygpath mangles remote paths...!!!
#        chomp(my $cygpath = `cygpath -ua $path`);
#        debug "'$path' becomes '$cygpath'", 'aP';
#        return $cygpath;
        # No change if path already starts with /cygdrive/
        return $path if $path =~ m|^/$Cygdrive/|;
        # Change C:\... to /cygdrive/c/... etc.; no other change
        # !! What if it's really a single-letter hostname?
        return $path if ($path =~ s|^([A-Za-z]):|"/cygdrive/".lc($1)|e);
        # No change if remote
        return $path if remote($path);
        # what happens now?
        return rel2abs($path);
    }
    # Not Windows:
    # NOTE: This is Unix-specific
    # See http://stackoverflow.com/questions/4404628/abs-path-with-the-home-directory
    # and http://docstore.mik.ua/orelly/perl4/cook/ch07_04.htm
    return $path if remote($path);
    $path =~ s{^~([^/]*)}{$1?(getpwnam($1))[7]:($ENV{HOME} || $ENV{LOGDIR} || (getpwuid($<))[7])}ex;
    my $abspath = rel2abs($path);
    debug "$path becomes $abspath", 'aP';
    return $abspath;
}

#---------------------------------------------------------------------------------------------------

sub rsyncAbsPath ($) {
    # A version of absPath that allows for the fact rsync uses ':' to 
    # indicate a remote location (and :: for a remote daemon).
    # FIXME and what about rsync://... style?
    # On Windows, we have convert C:\... to /cygdrive/c... etc. first.
    my $path = shift;
    return absPath($path);
    # FIXME -- always call absPath, so rsyncAbsPath seems redundant.  Is it on linux?
#    # FIXME what's going on?
#    #my $abspath = rel2abs($path);
#    debug "path=$path   r2a=".rel2abs($path)."   canon=".canonpath($path)."\n", 'rAP';
#    return $path if remote($path);
#    return $path if $path =~ /cygdrive/;    # W7 FIXME
#    return absPath($path);
}

#---------------------------------------------------------------------------------------------------

sub sourceDir ($) { 
    # Remote sources require an enclosing directory on the destination
    # to ensure that there is no overlap.
    # For example, two sources chris@host1:~/home and chris@host2:~/home
    # would both put files into /home on the destination without this.
    #
    # rsync deals with remote sources in the following formats:
    #   [USER@]HOST:SRC                 
    #   [USER@]HOST::SRC                
    #   rsync://[USER@]HOST[:PORT]/SRC  
    # In each case, the enclosing directory will just be HOST
    my $source = shift;
    return '' if !remote($source);
    my $dir;
    if ($source =~ m{^(rsync://|)((?<u>[^@]*)@)?(?<h>[^:/]+)}) {  
        $dir = "$+{h}";
    }
    return $dir;
}

#---------------------------------------------------------------------------------------------------

sub stdArgs ($) {
    # Return the standard arguments for rsync calls
    my $sending = shift;  # True for sending, false for receiving
    my $args = '';
    if ($Opt{'remote-rsync-cmd'}) {
        $args .= "--rsync-path '$Opt{'remote-rsync-cmd'}' ";
    }
    if ($sending) {
        $args .= "--bwlimit=$Opt{'bwlimit-out'} ";
    } else {
        $args .= "--bwlimit=$Opt{'bwlimit-in'} ";
    }
    return $args;
}

#---------------------------------------------------------------------------------------------------

sub updateSnapshotSizes () {
    # Make sure that each snapshot in %S has size information --
    # if not, get the size of the snapshot via rsync.
    # 
    # rsync returns directory listings thusly (even for recent dates, and even on Windows):
    #    -rwxr-xr-x         149 2008/06/13 13:56:30 textsmart
    #    drwxr-xr-x        4096 2011/05/07 17:41:48 40dnamer
    # and gives return code 23 if the directory is not found
    #                       255 for wrong password, unknown host etc.
    # Note the lack of nlink information, so we can't calculate 'delete size'.
    # Process in latest-first order so that we can always update the latest one
    # which will be non-zero if we restarted it.
    # W7 TODO C:\ looks like a remote host to rsync
# W7 initial scan to here
    my $firstOne = 1;
    foreach my $snapshot (rsort keys %S) {
        debug "initial snapshot size is $S{$snapshot}{size}\n", 'uSS';
        next if !$firstOne and $S{$snapshot}{size} >= 0;
        $firstOne = 0;
        my $totalSize = 0;
        # Get rsync listing into an array
        my @items;
        my @errors;
        my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'}, 
            stdArgs(0) . " --list-only --recursive '$S{$snapshot}{fullname}/'", 
            9,9, \@items, \@errors);    # FIXME let @errors be printed by sC
        debug "snapshot=$snapshot rc=$rc rc2='$rc2'\n", 'uSS';
        if (!$rc) {
            foreach my $item (@items) {
                debug "item = $item", 'uSS';
                # matching e.g.: 'drwxr-xr-x        4096 2011/05/07 17:41:48 40dnamer'
                if ($item =~ m|^
                    (?<t>.)
                    (?<p>.{9})\s+
                    (?<size>\d+)\s\d{4}/\d{2}/\d{2}\s\d{2}:\d{2}:\d{2}\s
                    (?<name>.+)$
                    |x) {
                    next if $+{name} eq '.';
                    next if $+{name} eq '..';
                    if ($+{t} eq '-' or $+{t} eq 'l' or $+{t} eq 'd') {
                        # file or symbolic link or directory
                        # (we're not following the symbolic link, 
                        # just counting the space of the link itself)
                        # (and we're counting the directory itself, not its contents)
                        debug "t=$+{t} size=$+{size} name=$+{name}\n", 'uSS';
                        $totalSize += diskSpace($+{size});
                    } else {
                        # (ignore anything other than files and directories)
                        debug "ignoring non-file-or-dir $item\n", 'uSS'
                    }
                } else {
                    # (ignore any non-matching lines)
                    debug "ignoring non-matching $item\n", 'uSS'
                }
            }
            $S{$snapshot}{size} = $totalSize;
            debug "adding size information for $S{$snapshot}{name}, size: $totalSize\n", 'uSS';
        } else {
            debug "rsync errors: @errors", 'uSS';
        }
    }
}

#---------------------------------------------------------------------------------------------------

sub rsyncMkdir ($) { 
    # Use rsync to create a directory.
    # Returns rsync return code -- zero if successful.
    my $newdir = shift;  # full path of dir to be created
    my @output;
    my @errors;
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'},
        stdArgs(1) . " -v --recursive --chmod=ug=rwx,o=rx $EmptyDir/ $newdir", 
        9,9, \@output, \@errors);
    debug "newdir='$newdir' rc=$rc rc2=$rc2 output='@output' errors='@errors'\n", 'rMd';
    # Note: no error if it's already there
    return $rc;
}

#---------------------------------------------------------------------------------------------------

sub rsyncRmdir ($$) {
    # Use rsync to remove a directory.  (seems to work on files too)
    # Returns rsync return code -- zero if successful.
    my $dir    = shift;  # full path of containing directory
    my $subdir = shift;  # subdir within dir to be deleted
    my @output;
    my @errors;
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'},
        stdArgs(1) . "--dirs -f '-!r $subdir' --delete $EmptyDir/ $dir",
        9,9, \@output, \@errors);
    debug "dir='$dir' subdir='$subdir' rc=$rc rc2=$rc2 output='@output' errors='@errors'\n", 'rRd';
    return $rc;
}

#---------------------------------------------------------------------------------------------------

sub rsyncExists ($) {
    # Use rsync to check if a directory or file exists.
    # Returns 1 if it does, 0 otherwise.
    my $item = shift;   # full path of directory or file
    my @output;
    my @errors;
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'},
        stdArgs(1) . " --list-only $item", 
        9,9, \@output, \@errors);
    debug "item='$item' rc=$rc rc2=$rc2 output='@output' errors='@errors'\n", 'rEx';
    # rc=23 if it doesn't exist.
    # FIXME what about other errors?
    return ($rc == 0 ? 1 : 0);
}

#---------------------------------------------------------------------------------------------------

sub rsyncReadable ($) {
    # Use rsync to check if a source is readable
    my $source = shift;
    my @output;
    my @errors;
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'},
        stdArgs(0) . "--list-only $source",
        9,9, \@output, \@errors);
    debug "source='$source' rc=$rc rc2=$rc2 output='@output' errors='@errors'\n", 'rR';
    return ($rc == 0); 
}

#---------------------------------------------------------------------------------------------------

sub rsyncWritable ($) {
    # Use rsync to check if a destination is writable
    my $dest = shift;
    my $tempdir = "$Script-temp-dir";    # is this ok?
    my @output;
    my @errors;
    #my $rc = rsyncMkdir("$dest/$tempdir");
    if ($Opt{'dry-run'}) {
        # Dry-run -- can't write to dest: just see if it's readable
        return rsyncReadable($dest);
    }
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'},
        stdArgs(1) . "--recursive $EmptyDir/ $dest/$tempdir/", 
        9,9, \@output, \@errors);
    debug "dest='$dest' tempdir='$tempdir' rc=$rc rc2=$rc2 output='@output' errors='@errors'\n", 
        'rW';
    return 0 if $rc;  # non-zero - it's not writable, return false
    # temp dir was created, so delete it
    rsyncRmdir($dest, $tempdir);
#    ($rc, $rc2) = systemCall($Opt{'rsync-cmd'},
#        stdArgs(1) . "--dirs -f '-!r $tempdir' --delete $EmptyDir/ $dest",
#        9,9, \@output, \@errors);
#    debug "delete rc=$rc\n", 'rW';
    return 1;
}

#---------------------------------------------------------------------------------------------------

sub rsyncFixPermsC ($$) {
    # Tweak the permissions of /cygdrive/c  (this is for cygwin only, obviously).
    # See e.g. http://tinyurl.com/dxucxg and http://tinyurl.com/2e7q6z8
    # for a discussion of the problem.
    # An alternative would be to set 'noacl' in cygwin's /etc/fstab, but that results in every
    # file being owned by the user who ran brandysnap.
    my $destdir = shift;
    my $args    = shift;
    debug "destdir='$destdir' args='$args'", 'rFPC';
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'},
        "$args -pr --chmod ugo=rwx $EmptyDir/ $destdir$CygdriveC",  9,9); 
    debug "rsync rc=$rc/$rc2\n", 'rFPC';
    if ($rc) {
        printlog "\nNOTE: Could not set the permissions of $destdir$CygdriveC\n" . 
        "      to sensible values: manual intervention will be required to access this snapshot\n", 
            1,1
    }

#---------------------------------------------------------------------------------------------------

sub lockName ($) {
    # Construct the name of a lock directory, given a pid
    my $pid = shift;
    return "$Opt{template}-lock-$pid";
}

#---------------------------------------------------------------------------------------------------

sub rsyncLockDest ($) {
    # Use rsync to create a lock directory on the destination, marking it as 'in use'.
    # The directory's name is '<template>-lock-<pid>', with the pid of the brandysnap process.
    # If any lock directories already exist and the pid is not running,
    # then delete the lock directory first.
    # If the pid is still running, then return 0 meaning "can't lock"
    my $dest = shift;
    my $locked = 0;
    my @output;
    my @errors;
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'},
        stdArgs(0) . "--list-only $dest/",
        9,9, \@output, \@errors);
    debug "dest='$dest' rc=$rc rc2=$rc2 output='@output' errors='@errors'\n", 'rLD';
    return 0 if $rc;   # Can't get directory list, so fail
    # Loop through listing, and deal with any lock dirs
    # looking for -rw-r--r--           0 2012/11/21 16:32:44 bstest-lock-123
    my $stillrunning = 0;
    foreach (@output) {
        my $lockdirpattern = lockName('(?<pid>\d+)');
        if (/$lockdirpattern/) {
            if (kill 0, $+{pid}) {
                # process is still running
                $stillrunning = 1;
            } else {
                # no process - remove the lock dir
                rsyncRmdir($dest, lockName($+{pid}));
                # (if that fails, the lock directory will be left there untidily,
                # but we can carry on with a new lock directory)
            }
        } 
    }
    if (!$stillrunning) {
        # Create a new lock directory with our pid
        if (rsyncMkdir("$dest/" . lockName($$))) {
            printlog "ERROR: Failed to create lock directory " . lockName($$) . "\n", 1,1;
        } else {
           $locked = 1;
        }
    }
    return $locked;
}

#---------------------------------------------------------------------------------------------------

sub rsyncUnlockDest ($) {
    # Use rsync to remove our lock directory from a destination.
    my $dest = shift;
    return rsyncRmdir($dest, lockName($$));
}

#===================================================================================================
# Handlings specs

sub calcPadding ($$$) {
    # Given a period level and a higher level and a time so far,
    # return the number of low-levels needed to get to the next high-level
    # e.g. ('d', 'm', 23days) returns 5 (days)
    # except that the levels are given numerically
    # Note that 'months' are always 28 days here.
    my $level1    = shift;
    my $level2    = shift;
    my $timesofar = shift;  # Time, counting backwards from 'now' in hours,
                            # that is already accounted for by previous specs
    my $length1 = $Hours[$level1];
    my $length2 = $Hours[$level2];
    my $result = ($length2 - ($timesofar % $length2)) / $length1;
    debug "l1=$level1 l2=$level2 tsf=$timesofar; returning $result\n", 'cP';
    if ($result != floor($result)) {
        debug "auto-padded spec is not aligned!\n", 'cP';
    }
    return floor($result);
}

#---------------------------------------------------------------------------------------------------

sub parseSpecs ($) {
    # Parse the user's requested snapshot regime, 
    # e.g. '0-4d7,1d7,2-7w,1m6'
    #
    # The letter specifies a period, and can be
    #   h - hour 
    #   d - day
    #   w - week
    #   m - month
    #   y - year
    # The first number ('freq') specifies how many snapshots should occur
    # within the period, the second how many periods.
    # The frequency can also be specified as a minimum-maximum range.
    # If the second number ('count') is omitted, it fills up the time
    # until the next spec; on the last spec, omitting the count
    # requests an 'unlimited' number of backups -- i.e. until the
    # backup disk is full.

    my $rawspec = lc(shift);
    debug "rawspec = '$rawspec'\n", 'pS';

    my @rawspecs = split /,/, $rawspec;
    my @specs = ();
    my $warningcount = 0;
    my $errorcount   = 0;

    foreach my $spec (@rawspecs) {
        #print "pS: before chomp: spec='$spec'  len=".length($spec)."\n";
        superchomp($spec);
        debug "spec = '$spec'\n", 'pS';
        #print "pS: after superchomp: spec='$spec'  len=".length($spec)."\n";
        if ($spec =~ /^$specRegex$/x) {
            my $freqmin = int($+{freqmin});
            my $freqmax = $freqmin;
            if ($+{freqmax}) {
                # min-max frequency specified
                $freqmax = int($+{freqmax});
            }
            if ($freqmax == 0) {
                printlog "ERROR: Frequency 0 in spec '$spec' does not make sense\n", 1;
                $errorcount += 1;
                next;
            }
            if ($freqmax < $freqmin) {
                printlog "ERROR: Maximum frequency must be greater than minimum: \n", 1;
                printlog "       '$freqmin-$freqmax' in spec '$spec' does not make sense\n", 1;
                $errorcount += 1;
            }
            my $period = $+{period};
            my $count; 
            if ($+{count} ne '') { 
                $count = int($+{count});
                if ($count == 0) {
                    printlog "ERROR: Count 0 in spec '$spec' will make no snapshots\n", 1;
                    $errorcount += 1;
                    next;
                }
            } else {
                # no count specified -- will use padding; set to 0 for now
                $count = 0;
            }   
            push @specs, {
                rawspec => $spec, 
                freqmin => $freqmin, 
                freqmax => $freqmax, 
                period  => $period, 
                periodn => $Level{$period}, # for convenience
                count   => $count,
            };  
        } else {
            printlog "ERROR: Invalid spec '$spec'\n", 1;
            $errorcount += 1;
        }
    }

    return ($warningcount, $errorcount, @specs) if $errorcount;

    debug "Parsed spec:  " . join(',', 
        map {fmtFreq($_->{freqmin}, $_->{freqmax}) . $_->{period} . $_->{count}} @specs)."\n", 'pS';

    # Check that the specs make sense
    debug "Checking specs...\n", 'pS';
    my $timesofar = 0; # in hours (because they're the smallest interval that we use)
    my $i = 0; # index into @specs
    foreach my $spec (@specs) {
        debug "Spec0: $spec->{freqmin}-$spec->{freqmax} " . 
            "$spec->{period} $spec->{count}     \n", 'pS';
        if ($spec->{count} == 0) {
            # Pad spec to fill next period
            # e.g. 1d,1w4 -- the daily period gets padded to a week, i.e. 1d7,1w4
            # This is done even if --calendar is false.
            if ($i == $#specs) {
                # last spec -- can't pad, so set count to lots
                printlog "NOTE: last spec padded forever\n", 3;
                $spec->{count} = UNLIMITED; 
            } elsif ($specs[$i+1]->{periodn} <= $spec->{periodn}) {
                #printlog "WARNING: Can not pad spec $spec->{rawspec} because next spec is " .
                #    "not at a higher level; count set to 1\n", 1;
                #$warningcount += 1;
                $spec->{count} = 1;
            } else {
                my $padding = calcPadding($spec->{periodn}, $specs[$i+1]->{periodn}, $timesofar);
                debug "padding $spec->{rawspec} to $LevelName[$specs[$i+1]->{periodn}]\n", 'pS';
                if ($padding == 0) {
                    # already at a multiple, so just do either 0 or a whole
                    # set of the next spec 
                    # leave at 0 for now
                    debug "No padding -- stay at 0\n", 'pS';
                } else {
                    # timesofar is not a whole multiple of speclevel+1, add some padding
                    debug "padding by setting count to $padding\n", 'pS';
                    $spec->{count} = $padding;
                }
            }
        }
        debug "Spec1: $spec->{freqmin}-$spec->{freqmax} $spec->{period} $spec->{count}\n", 'pS';
        # Check alignment
        if (!aligned($timesofar, $spec->{periodn})) {
            printlog "WARNING: Spec at $LevelName[$spec->{periodn}] level is not aligned - " 
                . " it starts at " . fmtHours($timesofar) . "\n", 1;
            $warningcount += 1;
        }
        $i += 1;
    }

    # Check that interval increases 
    my $lastint  = 0;
    $i = 0;
    foreach my $spec (@specs) {
        my $int = $Hours[$spec->{periodn}] / $spec->{freqmax};  # yes, use max
        if ($int < $lastint) {
            printlog "WARNING: Interval is smaller for spec " . ($i+2) . 
                " ($spec->{rawspec}) than for " .  "previous one ($specs[$i-1]->{rawspec})\n", 3;
            $warningcount += 1;
        }
        $lastint = $int;
        $i += 1;
    }

    if (!$errorcount) {
        printlog "Validated specs: " . join(',', 
            map {fmtFreq($_->{freqmin}, $_->{freqmax}) . $_->{period} . $_->{count}} @specs) . 
            "\n", 3;
        my $TotalSnapshotsMin = 0; 
        my $TotalSnapshotsMax = 0; 
        foreach my $spec (@specs) {
            $TotalSnapshotsMin += $spec->{freqmin} * $spec->{count};
            $TotalSnapshotsMax += $spec->{freqmax} * $spec->{count};
        }
        if ($specs[-1]->{count} == UNLIMITED) {
            printlog "which will create an unlimited number of snapshots\n", 3;
        } else {
            if ($TotalSnapshotsMin == $TotalSnapshotsMax) {
                printlog "which will create a total of $TotalSnapshotsMax snapshots\n", 3;
            } else {
                printlog "which will create a total of between $TotalSnapshotsMin and " . 
                    "$TotalSnapshotsMax snapshots\n", 3;
            }
        }
    }
    debug "Frequencies: " . 
        join(', ', map {$_->{freqmax} / $Hours[$_->{periodn}]} @specs) . "\n", 'pS';

    return $warningcount, $errorcount, @specs;  
}

#---------------------------------------------------------------------------------------------------

sub processSpecs ($) {
    # Validate the specs given 
    my $spec = shift;
    my ($warnings, $errors, @specs) = parseSpecs($spec); 
    my $msg;
    if ($errors) {
        $msg = "ERROR: One or more specs is invalid -- stopping";
    }
    if ($warnings and $Opt{strict}) {
        $msg = "ERROR: Warnings when parsing specs, and strict mode in effect -- stopping";
    }
#    if (!@specs) {
#        $msg =  "ERROR: No valid specs found -- stopping";
#    }
    return ($warnings, $errors, $msg, @specs);
}

#===================================================================================================

# Options 
# - defaults are in %defopt
# - then read command line options (including config file name) into %cmdopt
# - then read config file (if any) into %confopt
# - then merge defaults, config file, and command line options
# - then validate

#---------------------------------------------------------------------------------------------------

sub checkOptionSingle ($$) {
    # Check a single option/value for validity, returning the error and warning counts, 
    # and the possibly modified value.
    # Also returns any error/warning messages.
    # Doesn't do any cross-checking.
    my $option = shift;
    my $value  = shift;

    my $e   = 0;    # errors
    my $w   = 0;    # warnings
    my $msg = '';   # message(s)

    debug "option=$option value='$value'\n", 'cOS';
    if ($StringOpt{$option} or $MultiOpt{$option}) {    # Multi options are also strings
        # String options -- sort out any quotes
        $value = stripQuotes($value);
    } elsif ($LevelOpt{$option}) {
        # Output levels
        if (!looks_like_number($value)) {
            $msg .= "WARNING: Option '$option' should be a number from 1 to 9 -- value '$value'" .
               " treated as '1'\n";
            $value = 1;
            $w += 1;
        } else {
            $value = int($value);
            $value = 0 if $value < 0;
        }
    } elsif ($BoolOpt{$option}) {
        # Already converted to 0 or 1
    } elsif ($NumericOpt{$option}) {
        if (looks_like_number($value)) {
            $value = $value + 0;    # make it really numeric
        } else {
            $msg .=  "ERROR: Invalid option: '$option' must be numeric, not '$value'\n";
            $e += 1;
        }
    }

    if ($e == 0) {
        # Do option-specific checks
        for ($option) {
            # 'Best' options
            when ('hbest') {
                if (!($value >= 0 and $value <= 59)) {
                    $msg .=  "ERROR: Invalid option: 'hbest' must be a whole number between" . 
                        " 0 and 59, not '$value'\n";
                    $e += 1;
                }
            }
            when ('dbest') {
                if (!($value >= 0 and $value < 24.0)) {
                    $msg .=  "ERROR: Invalid option: 'dbest' must be a number between 0 and" . 
                        " 23.9, not '$value'\n";
                    $e += 1;
                }
            }
            when ('wbest') {
                if (!($value >= 1 and $value <= 7)) {
                    $msg .=  "ERROR: Invalid option: 'wbest' must be a whole number between" . 
                        " 1 and 7 (1=Sunday, 7=Saturday), not '$value'\n";
                    $e += 1;
                }
            }
            when ('mbest') { 
                if (!($value >= 1 and $value <= 31)) {
                    # (31 gives the last day of any month)
                    $msg .=  "ERROR: Invalid option: 'mbest' must be a whole number between" . 
                        " 1 and 31, not '$value'\n";
                    $e += 1;
                }
            }
            when ('ybest') { 
                if (!($value >= 1 and $value <= 366)) {
                    # (366 gives the last day of the year, whether a leap year or not)
                    $msg .=  "ERROR: Invalid option: 'ybest' must be a whole number between" .
                        " 1 and 366, not '$value'\n";
                    $e += 1;
                }
            }
            # Week start - an integer from 1=Sunday to 7=Saturday
            # (which is the same as $Opt{wbest} but different from localtime's $wday)
            when ('weekstart') {
                $value = 1 if !defined $value;
                $value = int($value);
                $value = (($value-1) % 7) + 1;  # force it into range
            }
            # Link-dest count -- positive integer
            when ('ldcount') {
                $value = int($value);
                if ($value < 0) {
                    $msg .=  "ERROR: Invalid option: 'ldcount' must be a positive whole number, " .
                        "not '$value'\n";
                    $e += 1;
                } elsif ($value == 0) {
                    $msg .=  "WARNING: Option 'ldcount' is zero -- no hard-linking will be done,\n";
                    $msg .=  "         and the snapshot created may be much larger than expected\n";
                    $w += 1;
                }
            }
            # Band-width limits (0 = unlimited)
            when (['bwlimit-in', 'bwlimit-out']) {
                if (!looks_like_number($value) or $value < 0) {
                    $msg .=  "ERROR: Invalid option: '$option' must be zero or a positive whole" . 
                        " number (in kbps), not '$value'\n";
                    $e += 1;
                } else {
                    $value = floor($value);
                }
            }
            # Actions to take if some or all sources are not backed up
            when (['all-failed', 'some-failed']) {
                if (index('keep', lc($value)) == 0) {   # allow abbreviations
                    $value = 'keep';
                } elsif (index('delete', lc($value)) == 0) {    # ditto
                    $value = 'delete';
                } else {
                    $msg .=  "ERROR: Invalid option: '$option' must be either 'keep' or" . 
                        " 'delete', not '$value'\n";
                    $e += 1;
                }
            }
            # Minimum interval between snapshots (in minutes)
            when ('min-interval') {
                if (!looks_like_number($value) or $value < 0) {
                    $msg .=  "ERROR: Invalid option: 'min-interval' must be a positive number" . 
                        " of minutes, not '$value'\n";
                    $e += 1;
                }
            }
            # Specs -- just check syntax
            when ('spec') {
                if (!($value =~ /^$specRegex(,$specRegex)*$/x)) {
                    $msg .= "ERROR: Spec '$value' is not valid\n";
                    $e += 1;
                }
            }

            # Source and destination are done later

            # Options 'include', 'include-from', 'exclude', 'exclude-from'
            # - these are not checked at all, just passed to rsync
        }
    }

    chomp($msg);
    #debug "returning e=$e w=$w value='$value' msg=$msg\n", 'cOS';
    return ($e, $w, $value, $msg);
}

#---------------------------------------------------------------------------------------------------

sub checkOptionArray($$) {
    # Check an option or array of options (from the command line)
    my $option = shift;
    my $valref = shift;
    my $errorcount   = 0;
    my $warningcount = 0;
    my $message      = '';
    if (ref($valref) eq 'ARRAY') {
        my @values;
        foreach my $value (@$valref) {
            my ($e, $w, $val, $msg) = checkOptionSingle($option, $value);
            $errorcount += $e;
            $warningcount += $w;
            push @values, $val;
            $message .= $msg;
        }
        $valref = [@values];
    } else {
        my ($e, $w, $val, $msg) = checkOptionSingle($option, $valref);
        $errorcount += $e;
        $warningcount += $w;
        $valref = $val;
        $message .= $msg;
    }
    return ($errorcount, $warningcount, $valref, $message);
}  

#---------------------------------------------------------------------------------------------------

sub getOptionsFromArray ($$$) {
    # Process command-line-like array of options into a hash,
    # using the global list of definitions
    my $argRef     = shift;
    my $optRef     = shift;
    my $GOarrayRef = shift;
    debug "args=@$argRef\n", 'gOFA';
    my $rc = 0;
    my @GOFAerrors;
    local $SIG{__WARN__} = sub { push @GOFAerrors, @_; };
    if (!GetOptionsFromArray($argRef, $optRef, @$GOarrayRef)) {
        # Print the error messages
        foreach my $error (@GOFAerrors) {
            printlog "ERROR: Unexpected option error: $error", 1;
        }
        $rc = @GOFAerrors;  # error count
    }
    return $rc;
}

#---------------------------------------------------------------------------------------------------

sub readConfig ($$) { 
    # Read in options from the given configuration filename
    # with a mixture of custom-validation and GetOptionsFromArray.
    # Returns non-zero if there were any errors,
    # and the hash of config file options.

    # Parse the config file, extracting options and values
    # The format is home-made and simple:
    # Definition:
    #   - ignore blank lines, and lines with first non-whitespace '#' (comments)
    #   - strip leading and trailing whitespace of whole line
    #   - option lines are of the form
    #       option  whitespace|:|= value
    #       option
    #       <option whitespace|:|= value>
    #           # options in new context go here
    #       </option>
    #   - option is non-whitespace i.e. a single word, with any leading - or -- stripped
    #   - value has surrounding whitespace stripped, but otherwise is anything
    #   - only 'destination' and 'source' can be used as contexts (see %ValidContext).

    state $cfile      = shift;     # ('state' to allow use in nested sub)
    state $GOarrayRef = shift;
    state $lineno = 0;
    #debug "cfile=$cfile GOarrayRef=$GOarrayRef\n", 'rC';
    
    # - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 

    sub confLine {
        return "\nin configuration file '$cfile' at line $lineno\n";
    }

    # - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 

    sub readContext {
        # Read options at the current level/context
        my $context = shift;
        my @options;
        # Hash to receive options from GOFA:
        my %confopt;
        my $rc = 0;
        LINE: while (<CONFIG>) {  # to end of context or end of file
            superchomp($_);
            $lineno += 1;
            debug "config line $lineno is '$_'\n", 'rC';
            next if /^\s*$/;	# Skip blank lines
            next if /^\s*#/;	# Skip comments

            if (/^\s*-*(?<no>no-?)?(?<option>[[:alpha:]-]+)\s*[:=]?\s*(?<value>.+)?\s*$/) {
                # Plain option line with possible 'no' prefix
                my $option = fullName($+{option});
                my $value = $+{value} // '';
                if ($BoolOpt{$option}) {
                    if ($value) {
                        printlog "ERROR: Option '$option' should not have a value; " .
                            "use '--no$option' turn it off" . confLine(), 1;
                        $rc += 1;
                        next LINE;
                    }
                    $value = $+{no} ? 0 : 1;
                } else {  # not boolean, so restore the 'no' prefix
                    $option = 'no' . $option if $+{no};
                }
                (my $e, my $w, $value, my $msg) = checkOptionSingle($option, $value);
                #debug "cOS returns $e, $w, '$value', '$msg'\n", 'rC';
                if (!$AllOpt{$option}) {
                    printlog "ERROR: Unknown option '$option'" . confLine(), 1
                        if $option ne '!*ambiguous';
                    $rc += 1;
                } elsif (!$Matrix{$context}{$option}) {
                    printlog "ERROR: Option '$option' not allowed within context '$context'" 
                        . confLine(), 1;
                    $rc += 1;
                } elsif ($e) {
                    printlog $msg . confLine(), 1,1;
                    $rc += $e; 
                } elsif ($ValidContext{$option}) {
                    # These are treated separately so that, for example,
                    #   destination d1
                    # is treated as
                    #   <destination d1></destination>
                    if (!$value) {
                        printlog "ERROR: Option '$option' must have a value " . confLine(), 1;
                        $rc += 1;
                    } else {
                        push @{$confopt{$option}}, {location => $value, options => {}};
                        debug "adding simple source/dest '$value'\n", 'rC';
                    }
                } else {
                    # Simple option -- just add it to the list
                    # First, print any warnings
                    printlog $msg . confLine(), 1,1 if $w;
                    if ($BoolOpt{$option}) {
                        debug "restoring no?: option=$option value=$value\n", 'rC';
                        push @options, '--' . ($value ? '' : 'no') . $option;
                    } else {
                        debug "storing option=$option value=$value\n", 'rC';
                        push @options, "--$option";
                        push @options, $value;
                    }
                    debug "$option = $value, now options=@options\n", 'rC';
                }

            } elsif (/^\s*<\s*(?<option>[[:alpha:]-]+)\s*[:=]?\s*(?<value>.+)?\s*>/) {
                # Start of new context
                my $option = fullName($+{option});
                my $value = $+{value} // '';
                debug "\n>>>>>> new context=$option  \n", 'rC';
                (my $e, my $w, $value, my $msg) = checkOptionSingle($option, $value);
                if (!$ValidContext{$option}) {
                    printlog "ERROR: Unknown context '$option' -- context skipped" .
                        confLine(), 1;
                    $rc += 1;
                } elsif (!$AllowedContext{$context}{$option}) {
                    printlog "ERROR: Context '$option' not allowed within context '$context'" . 
                        " -- context skipped" . confLine(), 1;
                    $rc += 1;
                } elsif ($e) {
                    printlog $msg . confLine(), 1,1;
                    $rc += $e; 
                } else {
                    #if (!$value) {
                    #    printlog "ERROR: Option '$option' must have a value" . 
                    #        confLine() . "\n", 1;
                    #    $rc += 1;
                    #} else {
                    # First, print any warnings
                    printlog $msg . confLine(), 1,1 if $w;
                    # OK, it's a valid context (source or destination) - recurse
                    my ($newrc, $newConfRef) = readContext($option);
                    push @{$confopt{$option}}, {location => $value, options => $newConfRef};
                    debug "adding complex destination '$value'\n", 'rC';
                    $rc += $newrc;
                    #}
                }

            } elsif (/^\s*<\/\s*(?<option>[[:alpha:]]*)[^>]*>/) {
                # End of context: ignore the name -- just match on any </...>
                if ($context eq 'general') {
                    # end of context is an error 
                    printlog "ERROR: Unmatched end-of-context: '$+{option}'" .
                        confLine(), 1;
                    $rc += 1;
                } else {
                    # Finish this level of recursion
                    debug "<<<<<< end of context (option=$+{option})\n\n", 'rC';
                    last;
                }

            } else {
                printlog "ERROR: Invalid line: '$_'" . confLine(), 1;
                $rc += 1;
            }

        } # while

        # Call GOFA to merge @options with %confopt
        debug "gofa gets: @options\n", 'rC';
        my $gofarc = getOptionsFromArray(\@options, \%confopt, $GOarrayRef);
        debug "gofarc = $gofarc\n", 'rC';
        $rc += $gofarc;

        return ($rc, \%confopt);

    } # end of readContext()

    # - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 

    if (!open CONFIG, '<', $cfile) {
        # Shouldn't happen as we've already tested for readability 
        printlog "ERROR: failed to open configuration file '$cfile'\n", 1;
        return (1, {});
    }
    my ($rc, $confoptRef) = readContext('general');
    close CONFIG;

    debug "confopt:\n", 'rC';
    debug Dumper($confoptRef), 'rC';
    return ($rc, %$confoptRef);
}

#---------------------------------------------------------------------------------------------------

sub mergeOpts (@) {
    # Merge option hashes together
    my %Opt = %{(shift)};
    foreach my $nextoptref (@_) {
        foreach my $k (keys %$nextoptref) {
            $Opt{$k} = $nextoptref->{$k};
        }
    }
    return %Opt;
}

#---------------------------------------------------------------------------------------------------

sub processOptions () {

    # Default options hard-coded here
    # -------------------------------
    my %defopt = ();    # default options
    # output: for each of these, 0 means no (or minimal) output, 3 means lots, 
    # (higher values for debugging only)
    $defopt{verbose}        = 1;
    $defopt{loglevel}       = 1;
    #
    $defopt{debug}          = '';
    $defopt{test}           = 0;          # a non-zero value indicates which test to run
    # xbest options to prefer one part of a period, e.g. 5pm, Fridays
    # defaults are at the end of the period (calendar mode only!)
    $defopt{hbest}          = 59;         # preferred minute within an hour 0-59, integer
    $defopt{dbest}          = 23.99;      # preferred hour within a day 0-23.99, float
    $defopt{wbest}          = 7;          # preferred day within a week 1-7, integer, 1=Sun 7=Sat
    $defopt{mbest}          = 31;         # preferred day within a month 1-31  
    $defopt{ybest}          = 366;        # preferred day in the year 1-366
    # 
    $defopt{'all-failed'}   = 'delete';
    $defopt{'allow-restart'} = 1;   
    $defopt{'bwlimit-in'}   = 0;        
    $defopt{'bwlimit-out'}  = 0;
    $defopt{calendar}       = 1;
    $defopt{compress}       = 1;
    $defopt{delete}         = 1;
    $defopt{'delete-cp'}    = 1;
    $defopt{'dry-run'}      = 0;
    $defopt{'expire-old'}   = 0;          # expire oldest S's to make room, even when not unlimited
    $defopt{help}           = 0;
    $defopt{ldcount}        = 1;          # no. of link-dest options to create
    $defopt{'min-interval'} = 0;          # minutes between snapshots
    $defopt{restart}        = 0;
    $defopt{'rsync-cmd'}    = 'rsync';    # Should it be '/usr/bin/rsync'?
    $defopt{'rsync-opts'}  = '--hard-links --numeric-ids --archive --one-file-system --timeout=300';
    $defopt{safe}           = 1;        
    $defopt{snapshot}       = 1;
    $defopt{'some-failed'}  = 'keep';
    $defopt{status}         = 0;
    $defopt{strict}         = 0;
    $defopt{version}        = 0;
    $defopt{weekstart}      = 1;          # 1=Sunday
    # The following deliberately do not have defaults:
    #   template   source  logfile   config  destination  spec
    #   include include-from exclude exclude-from
    #   remote-rsync-cmd

    my %confopt = ();   # config file options
    my %cmdopt = ();    # command line options

    my @GOarray = (     # array to pass to GetOptions and GetOptionsFromArray
            'all-failed=s',
            'allow-restart!',
            'bwlimit-in=f',
            'bwlimit-out=f',
            'calendar!',
            'compress!',
            'config=s',
            'dbest=f',
            'debug=s',      
            'delete!',
            'delete-cp!',
            'destination=s@',
            'dry-run!',
            'exclude-from=s@',
            'exclude=s@',
            'expire-old!',
            'hbest=i',
            'help!',
            'include-from=s@',
            'include=s@',
            'ldcount=i',
            'lockfile=s',
            'logfile=s',
            'loglevel=i',
            'mbest=i',
            'min-interval=f',
            'restart!',
            'rsync-cmd=s',
            'rsync-opts=s',
            'remote-rsync-cmd=s',
            'safe!',
            'snapshot!',
            'some-failed=s',
            'source=s@',
            'spec=s',
            'status!',
            'strict!',
            'template=s',
            'test=i',
            'verbose|verbosity=s',
            'version',
            'wbest=i',
            'weekstart=i',
            'ybest=i',
    );

    my $errorcount = 0;
    my $warningcount = 0;

    # Block to limit the scope of the SIGWARN override which traps warnings from GetOptions
    {
        # Get command line options -- give up if any errors.
        my @GOerrors;
        local $SIG{__WARN__} = sub { push @GOerrors, @_; };
        my $GOok = GetOptions(\%cmdopt, @GOarray);
        if (!$GOok) {
            foreach my $error (@GOerrors) {
                printlog "ERROR: $error", 1;
            }
            $errorcount += @GOerrors;
        }
        if (@ARGV) {
            printlog "ERROR: Unexpected word(s) on the command line: " .
                join(', ', map {"'$_'"} @ARGV) . "\n", 1;
            $errorcount += @ARGV;
        }
        return $errorcount if $errorcount;
    }

    # Validate command-line options 
    foreach my $option (sort keys %cmdopt) {
        my ($e, $w, $value, $msg) = checkOptionArray($option, $cmdopt{$option});
        debug "cOA for $option returns $e, $w, '$value', '$msg'\n", 'pO';
        $errorcount += $e;
        $warningcount += $w;
        $cmdopt{$option} = $value;
        printlog "$msg (on command line)\n", 1 if $msg;
    }
    return $errorcount if $errorcount;

    # Tweak context options from command line to make them into config-file structures
    foreach my $context (keys %ValidContext) {
        if ($cmdopt{$context}) {
            my @temp;
            foreach my $c (@{$cmdopt{$context}}) {
                push @temp, {location => $c, options => {}};
            }
            $cmdopt{$context} = \@temp;
        }
    }

    # If config file specified, check it and read it
    if ($cmdopt{config}) {
        $cmdopt{config} = absPath($cmdopt{config});
        if (-f $cmdopt{config} && -r _) {
            (my $rc, %confopt) = readConfig($cmdopt{config}, \@GOarray);    
            return $rc if $rc;
        } else {
            printlog "ERROR: Configuration file '$cmdopt{config}' can not be read\n", 1,1;
            return 1;
        }
        printlog "\nConfiguration file is '" . hostname . ":$cmdopt{config}'\n\n", 1,1;
    }

    debug "def  options: " . join(' ', 
        map {$_ . '=' . $defopt{$_}}  sort keys %defopt)  . "\n", 'pO';
    debug "conf options: " . join(' ', 
        map {$_ . '=' . $confopt{$_}} sort keys %confopt) . "\n", 'pO';
    debug "cmd  options: " . join(' ', 
        map {$_ . '=' . $cmdopt{$_}}  sort keys %cmdopt)  . "\n", 'pO';
    
    # Merge options, so that config file options override the defaults,
    # and command-line options override everything else
    %Opt = mergeOpts(\%defopt, \%confopt, \%cmdopt);

    # Split comma-separated debug values into a hash
    $Opt{debug} = {map {lc($_) => 1} split(/,/, $Opt{debug})};
    
    debug "Partially validated final options: " . join(' ', 
        map {$_ . '=' . $Opt{$_}} sort keys %Opt) . "\n", 'pO';
    
    # If user asked for help or version, or is running test mode,
    # don't do any more validation
    if ($Opt{help} or $Opt{version} or $Opt{test}) {
        return 0;
    }

    # Check for missing options at general level
    foreach my $ropt (@ReqOpt) {
        unless ($Opt{$ropt}) {
            printlog "ERROR: Required option '$ropt' is missing\n", 1;
            $errorcount += 1;
        }
    }

    # Logging (check this next, so that we can log later errors).
    # Note that, once validated, the 'logfile' option is copied to
    # $Opt{validLogfile}, which is what is used by logit().
    # The logfile must be local.
    if (!$Opt{logfile}) {
        # No logfile, so no logging
        $Opt{loglevel} = 0;
    } else {
        $Opt{logfile} = absPath($Opt{logfile});
        # Open and lock the logfile
        if (open LOGFILE, '>>', $Opt{logfile}) {
            if (flock(LOGFILE, LOCK_EX | LOCK_NB)) {
                $Opt{validLogfile} = $Opt{logfile};
                # Make the logfile unbuffered
                my $oldfh = select LOGFILE;
                $| = 1;
                select $oldfh;
            } else {
                printit "\nCan not lock logfile '$Opt{logfile}'.\n" .
                    "$Script is probably already running with that logfile.\n\n", 1;
                    #"If you're sure it isn't, delete the lock-file and try again.\n", 1;
                close LOGFILE;
                exit EXITNOLOCK;
            } 
        } else {
            printit "ERROR: Logfile '$Opt{logfile}' (option 'logfile') can not be written\n", 1;
            $Opt{loglevel} = 0;
            $errorcount += 1;
        }
    }

    return $errorcount if $errorcount;

    # Add basic info to logfile (which couldn't be done before we had a valid logfile)
    my $intro = "$Script version $VERSION starting at " . (strftime "%H:%M:%S", localtime);
    logit under2("\n\n$intro\n\n"), 1;
    logit "Command line: $0 @ARGVcopy\n", 3;
    # TODO code for checking readable sources should be factorised
    #      because it's used here and per-destination
    #      especially if it is extended to have required options
    #      at source level.
    # Sources, with extra options for each
    # (there may be no sources at general level)
    my $readableSources = 0;
    if ($Opt{source}) {
        my @tempSources = @{$Opt{source}};
        @{$Opt{source}} = ();
        foreach my $src (@tempSources) {
            my $abssrc = rsyncAbsPath($src->{location});
            if (rsyncReadable($abssrc)) {
                $readableSources += 1; 
                push @{$Opt{source}}, {location=>$abssrc, options=>$src->{options}};
            } else {
                printlog "WARNING: Source '$abssrc' can not be read:\n" .
                         "         it will be ignored\n", 1;
                $warningcount += 1;
                # (and don't copy it to the final array)
            }
        }
    }

    # lockfile option is obsolete
    if ($Opt{lockfile}) {
        printlog "NOTE: Option 'lockfile' is no longer used, and will be ignored.\n" .
                 "      It will become invalid in the future: please remove it.\n";
        delete $Opt{lockfile};
    }

    # Check for required options per destination
    foreach my $dest (@{$Opt{destination}}) {
        foreach my $ropt (@DestReqOpt) {
            #debug "checking $ropt for dest $dest->{location}\n", 'pO';
            unless ($Opt{$ropt} or ${$dest->{options}}{$ropt}) {
                printlog "ERROR: Required option '$ropt' is missing " .
                    "for destination '$dest->{location}'\n", 1;
                $errorcount += 1;
            }
        }
    }
    return $errorcount if $errorcount;

    # Parse general-level specs
    if ($Opt{spec}) {
        my ($w, $e, $msg, @pspecs) = processSpecs($Opt{spec});
        $errorcount += $e;
        $warningcount += $w;
        printlog "$msg\n" if $msg;
        $Opt{pspecs} = \@pspecs if !$e;
    }

    # Destination directories (with extra options for each)
    my $writableDests = 0;
    my @tempDests = @{$Opt{destination}};
    @{$Opt{destination}} = ();
    foreach my $dest (@tempDests) {
        # Check that destination is writable
        my $absdest = rsyncAbsPath($dest->{location});
        #debug "destination: absdest=$absdest\n", 'pO';
        if (rsyncWritable($absdest)) {
            my $options = $dest->{options};
            my $readableSubSources = 0;
            #debug "checking sub sources for $absdest\n", 'pO';
            #debug "options = " . Dumper($options), 'pO';
            if ($options->{source}) {
                # This destination has its own source(s) which
                # will override any at the general level
                my @tempSources = @{$options->{source}};
                @{$options->{source}} = ();
                foreach my $src (@tempSources) {
                    my $abssrc = rsyncAbsPath($src->{location});
                    #debug "checking $abssrc within $absdest\n", 'pO';
                    if (rsyncReadable($abssrc)) {
                        $readableSubSources += 1; 
                        #debug "good source $abssrc\n", 'pO';
                        push @{$options->{source}}, {location=>$abssrc, options=>$src->{options}};
                    } else {
                        printlog "WARNING: Source '$abssrc' can not be read: \n" . 
                                 "         it will be ignored\n", 1;
                        $warningcount += 1;
                        # (and don't copy it to the final array)
                    }
                }
            } else {
                # This destination has no sources of its own
                #debug "no sub sources for $absdest\n", 'pO';
                $readableSubSources = $readableSources;
            }
            if ($readableSubSources) {
                $writableDests += 1;
                # It's a good destination, so parse any spec
                if ($options->{spec}) {
                    my ($w, $e, $msg, @pspecs) = processSpecs($options->{spec});
                    $errorcount += $e;
                    $warningcount += $w;
                    printlog "$msg\n" if $msg;
                    $options->{pspecs} = \@pspecs if !$e;
                }
                # Store the destination back in the main options hash
                push @{$Opt{destination}}, {location=>$absdest, options=>$options};
            } else {
                printlog "WARNING: None of the sources specified for destination '$absdest'" . 
                    " are readable\n", 1;
                printlog "         The whole destination will be ignored\n", 1;
            }
        } else {
            printlog "WARNING: Destination '$absdest' is not available\n" .
                     "         as a writable directory: it will be ignored\n", 1;
            $warningcount += 1;
        }
    }
    if ($writableDests == 0) {
        printlog "ERROR: None of the destinations specified is available\n", 1;
        $errorcount += 1;
    }
    return $errorcount if $errorcount;

    # Cross-check sources and destinations -- they can't all be remote
    # 1. for each destination
    #       if local sources, check them
    #       else check top-level sources
    #          i.e. override top-level temporarily if any
    my $somethingToDo = 0;
    #debug "opt-dest = " . Dumper($Opt{destination}), 'pO';
    foreach my $dest (@{$Opt{destination}}) {
        #debug "checking $dest->{location}\n", 'pO';
        my $remoteDest = remote($dest->{location});
        my $source;
        #debug "dest sources = " . Dumper($dest), 'pO';
        if (exists $dest->{options}->{source}) {
            $source = $dest->{options}->{source};
            #debug "dest sources = " . Dumper($source), 'pO';
        } else {
            $source = $Opt{source};
            #debug "global sources = " . Dumper($source), 'pO';
        }
        foreach my $src (@{$source}) {
            #debug "x-checking $dest->{location} -- $src->{location}", 'pO';
            my $remoteSrc = remote($src->{location});
            if (!remote($src->{location}) or !$remoteDest) {
                $somethingToDo = 1;
                #debug " -- OK\n", 'pO';
            } else {
                #debug " -- Not\n", 'pO';
            }
        }
    }
    if (!$somethingToDo) {
        printlog "ERROR: All destination/source combinations are remote/remote -- " . 
            "can not continue\n", 1;
        $errorcount += 1;
    }
    return $errorcount if $errorcount;
    
    # Set up summary structure for results
    foreach my $dest (@{$Opt{destination}}) {
        $Summary{$dest->{location}}{$DestResult} = 'not attempted';
        if ($dest->{options}->{source}) {
            # destination has its own sources -- use them
            foreach my $source (@{$dest->{options}->{source}}) {
                $Summary{$dest->{location}}{$source->{location}} = 'not attempted';
            }
        } else {
            # use general sources
            foreach my $source (@{$Opt{source}}) {
                $Summary{$dest->{location}}{$source->{location}} = 'not attempted';
            }
        }
    }

    # --restart implies --snapshot and --min-interval=0
    $Opt{snapshot} = 1 if $Opt{restart};
    $Opt{'min-interval'} = 0 if $Opt{restart};

    # Options 'include', 'include-from', 'exclude', 'exclude-from'
    # - these are not checked at all, just passed to rsync

    # spec is checked later

    return $errorcount if $errorcount;

    debug "Validated final options: " . join(' ', 
        map {$_ . '=' . $Opt{$_}} sort keys %Opt) . "\n", 'pO';

    return $errorcount;
}

#---------------------------------------------------------------------------------------------------

sub printableOption ($$;$) {
    # Return a printable version of an option and its value,
    # with the option aligned right in the given width
    my $opt   = shift;
    my $val   = shift;
    my $width = shift // 0;
    debug "opt=$opt  val=$val  width=$width\n", 'prO';
    my $val2 = '';
    # (arrays are dealt with by the caller)
    if (ref($val) eq 'HASH') {
        # hash option -- just 'debug' so far
        if (keys %$val) {
            $val2 = join(',', map {"$_"} sort keys %$val); 
        } else {
            $val2 = '(none)';
        }
    } elsif ($BoolOpt{$opt}) {
        # boolean options
        $val2 = $val ? 'yes' : 'no';
        $opt = ($val ? '' : 'no') . $opt;
        undef $val2;
    } elsif ($StringOpt{$opt} or $MultiOpt{$opt}) {
        # string option, add quotes
        $val2 = "'$val'";
    } else {
        # else it's numeric, no quotes
        $val2 = $val;
    }
    if (defined $val2) {
        return sprintf("%*s: %s", $width, $opt, $val2);
    } else {
        return sprintf("%*s", $width, $opt);
    }
}

#---------------------------------------------------------------------------------------------------

sub displayContext ($$$);   # pre-declaration required for recursion
sub displayContext ($$$) {
    # Display the sub-options for a single context
    my $context = shift;    # the name of the context option
    my $contRef = shift;    # the context's sub-options
    my $indent  = shift;    # for pretty-printing
    my $text = '';
    foreach my $cref (@{$contRef->{$context}}) {
        if (keys %{$cref->{options}}) {
            $text .= sprintf("%*s: '%s'>\n", $indent+16, "<$context", $cref->{location});
            my $copts = $cref->{options};
            my $text2 = '';    # put the sub-contexts last
            foreach my $subopt (sort keys %$copts) {
                next if $subopt eq 'pspecs';       # for internal use only
                if ($AllowedContext{$context}{$subopt}) {
                    $text2 .= displayContext($subopt, $copts, $indent+13);
                } else {
                    if (ref($copts->{$subopt}) eq 'ARRAY') {
                        foreach my $val (@{$copts->{$subopt}}) {
                            $text .= printableOption($subopt, $val, $indent+16+13) . "\n";
                        }
                    } else {
                        $text .= printableOption($subopt, $copts->{$subopt}, $indent+16+13) . "\n";
                    }
                }
            }
            $text .= $text2;    
            $text .= sprintf("%*s\n", $indent+16+2, "</$context>");
        } else {
            # No sub-options, just display as a simple option
            $text .= sprintf("%*s: '%s'\n", $indent+16, "$context", $cref->{location});
        }
    }
    return $text;
}
 
#---------------------------------------------------------------------------------------------------

sub displayOptions () {
    # Display the options nicely formatted
    my $optSummary = "\nOptions Summary:\n";
    foreach my $opt (sort keys %Opt) {
        next if $opt eq 'validLogfile'; # for internal use only
        next if $opt eq 'pspecs';       # for internal use only
        my $text = '';
        if ($ValidContext{$opt}) {
            $text = displayContext($opt, \%Opt, 0);
        } else {
            if (ref($Opt{$opt}) eq 'ARRAY') {
                foreach my $val (@{$Opt{$opt}}) {
                    $text .= printableOption($opt, $val, 16) . "\n";
                }
            } else {
                $text = printableOption($opt, $Opt{$opt}, 16) . "\n";
            }
        }   
        $optSummary .= $text;
    }
    printlog "$optSummary\n", 2,2;
}

#===================================================================================================

# Help

sub showLicence () {
    printit <<EOT;
rsync-based backup snapshot creation and management        
Copyright (C) 2011,2012 Chris Dennis, chris\@starsoftanalysis.co.uk        
                                                                       
This program is free software: you can redistribute it and/or modify  
it under the terms of the GNU General Public License as published by  
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.                              
                                                                  
This program is distributed in the hope that it will be useful,     
but WITHOUT ANY WARRANTY; without even the implied warranty of     
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the        
GNU General Public License for more details.                          
                                                                      
You should have received a copy of the GNU General Public License     
along with this program.  If not, see <http://www.gnu.org/licenses/>. 

EOT
}

#---------------------------------------------------------------------------------------------------

sub showHelp () {
    printit <<EOT;

Usage: $Script [option]...

Options can be specified on the command line or in the configuration file.
Main options
--config       file          configuration file name 
--source       file/dir !*d  local or remote file or directory to add to snapshot
--destination  dir      !*   local or remote snapshot destination
--template     name     ! d  prefix for snapshot directories 
--spec         ...      !    snapshot-keeping specification 
--logfile      file          file used for logging 
Tuning options
--[no]calendar            d  use calendar mode (default: calendar)
--[no]safe                d  use safe mode (default: safe)
  Note: --<x>best options only apply to calendar mode.
--hbest        0..59      ds favoured minute within hour (default: 59) 
--dbest        0..23.9    ds favoured hour within day in hours (default: 23.9) 
--mbest        1..31      ds favoured day within month (default: 31) 
--wbest        1..7       ds favoured day within week, 1=Sunday (default: 1) 
--ybest        1.366      ds favoured day within year (default: 366) 
--min-interval 0..        d  minimum number of minutes between snapshots (default: 0) 
--weekstart    1..7       d  first day of week, 1=Sunday (default: 1) 
Helpful options
--help                       display this help message and stop
--version                    display version number only stop
--verbose      0..3       ds amount of output displayed (default: 1) 
--loglevel     0..3       ds amount of logging information (default: 1) 
--[no]dry-run                make no actual changes (default: nodry-run) 
Rsync options
--rsync-cmd    file       ds location of the rsync programme (default: rsync) 
--[no]compress yes/no     ds enable rsync compression for remote transfers 
                              (default: compress) 
--include      pattern   *ds rsync inclusion (see rsync documentation)
--include-from file      *ds rsync inclusions 
--exclude      pattern   *ds rsync exclusion
--exclude-from file      *ds rsync exclusions 
--bwlimit-in   n          ds bandwidth limit for receiving in kbps (default: 0) 
                              (set to 0 for no limit) 
--bwlimit-out  n          ds bandwidth limit for sending in kbps (default: 0) 
                              (set to 0 for no limit) 
--rsync-opts   options    ds extra options to pass to rsync 
                              (default: -aHx --numeric-ids) 
--remote-rsync-cmd file   ds location of the rsync programme on the remote
                               computer (optional, no default) 
Advanced options
--all-failed  keep|delete d  (default: delete) 
--some-failed keep|delete d  (default: keep) 
                             what to do with the snapshot if all or some of the 
                              sources are not copied successfully
                              (values can be abbreviated)
--[no]allow-restart       ds keep the unfinished snapshot if brandysnap was
                              interrupted (e.g. by ctrl-C or system shutdown)
                              (default: allow-restart)
--[no]delete              d  delete no-longer-required snapshots (default: delete) 
--[no]delete-cp           d  delete even in current period (default: delete-cp)
--[no]expire-old          d  delete oldest snapshots when disk is full (default: no-expire-old)
--ldcount      n          d  number of --link-dest options to create (default: 1) 
--[no]restart             ds continue snapshot(s) from previous run (default: norestart)
                              (implies --snapshot) 
--[no]snapshot            d  create new snapshots (default: snapshot)
--status                     print a status report only
--[no]strict              d  use strict mode (default: nostrict)
Development options
--debug    code[,code...] ds sections in which to enable debugging (default: none) 
--test         n             run test case 'n' 
Options marked with '!' are required -- others have sensible default values.
Options marked with '*' can be specified more than once.
Options marked with 'd' can be specified within a <destination> section.
Options marked with 's' can be specified within a <source> section.

Please see the $Script documentation for full details, including the use of
'contexts' in the configuration file to allow, for example, an exclude
option to apply to one particular source.

EOT
    showLicence();
}
#===================================================================================================

# Snapshot stuff 

#---------------------------------------------------------------------------------------------------

sub deleteSnapshot ($) {
    # Delete a snapshot directory, returning the number of errors encountered.
    # Uses rsync to do the deleting, even for local snapshots, because that
    # avoids problems with permissions.
    my $snapshot = shift; 
    debug "doing $S{$snapshot}{fullname}\n", 'dS';
    if ($Opt{'dry-run'}) {
        printlog "Dry run: would have deleted '$S{$snapshot}{name}'\n", 1;
        return 0;
    }
    if (!$Opt{delete}) {
        printlog "Not deleting snapshot '$S{$snapshot}{name}' because of option 'delete=no'\n", 1;
        return 0;
    }
    # Use some rsync trickery to delete the snapshot
    # (gleaned from http://lists.samba.org/archive/rsync/2010-October/025723.html)
    # This involves copying an empty directory onto the snapshot, and
    # removing the snapshot directory itself with filter cleverness.
    my @output;
    debug "deleting snapshot the new way\n", 'dS';
    #my $remPath = $Opt{'remote-rsync-cmd'} ? "--rsync-path '$Opt{'remote-rsync-cmd'}'" : '';
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'}, 
        stdArgs(1) . " --dirs -f 'R /$S{$snapshot}{name}/**' -f '-!r /$S{$snapshot}{name}' " .
        "--delete $EmptyDir/ $S{$snapshot}{path}", 
        9,9, \@output); 
    debug "rsync output: \n@output", 'dS';
    debug "rsync returned $rc, '$rc2'\n", 'dS';
    # This returns no error if the snapshot didn't exist, which is fine
    if ($rc) {
        printlog "ERROR: failed to delete snapshot '$S{$snapshot}{name}' " .
            "from '$S{$snapshot}{path}' (code $rc)", 1,1;
        return 1;
    }
    delete $S{$snapshot};   # delete it from %S too
    return 0;
}

#---------------------------------------------------------------------------------------------------

sub deleteSnapshots (\@\@$) {
    # Delete the snapshots from the given list (latest first)
    my @D         = @{(shift)}; # list of snapshots to delete
    my @cSpecs    = @{(shift)};
    my $deleteNew = shift;
    return if !@D;
    my $ci = 0; # index into @cSpecs
    my $errors = 0;
    printlog "\n", 1;
    debug "list is: " . (join(' ', map {$S{$_}{name}} @D)) . "\n", 'dS';
    return 0 if !$Opt{delete};
    foreach my $di (0..$#D) {
        my $d = $D[$di];
        # Find the cSpec matching this snapshot (just for the log message)
        while ($cSpecs[$ci]->{beg} > $d) { 
            $ci += 1; 
        }; 
        if ($di == 0 and ($deleteNew)) {
            # Don't need to delete the new snapshot because we avoided creating it
            debug "not deleting newest snapshot because it was not created\n", 'dS';
        } else {
            my $unfinished = $S{$d}{unfinished} ? 'unfinished ' : '';
            if ($cSpecs[$ci]->{label} eq 'old') {
                printlog "Deleting old ${unfinished}snapshot '$S{$d}{name}'\n", 1;
            } else {
                printlog "Deleting ${unfinished}snapshot '$S{$d}{name}'" . 
                " because there are too many for spec '" . $cSpecs[$ci]->{label} . "'\n", 1;
            }
            $errors += deleteSnapshot($d);
        }
    }
    #printlog "Total of $errors errors while deleting snapshots!\n", 1; 
    return $errors;
}

#---------------------------------------------------------------------------------------------------

sub deleteOldSnapshots (\@;$) {
    # Delete $n snapshots from the list of old ones (oldest ones first)
    my $expirablesR = shift;      # use ref, don't copy array, so that it gets popped
    my $n           = shift // 1;
    my $deleted = 0;
    debug "n=$n  expirables=@$expirablesR\n", 'dOS';
    while (@$expirablesR and $n > 0) {
        my $e = pop @$expirablesR;
        $n -= 1;
        printlog "Expiring old snapshot $S{$e}{name}\n", 2;
        if (deleteSnapshot($e) == 0) {  
            $deleted += 1;
        } else {
            debug "failed to delete snapshot $S{$e}{name}\n", 'dOS';
        }
    }
    debug "deleted $deleted oldest snapshots\n", 'dOS';
    return $deleted;
}

#---------------------------------------------------------------------------------------------------

sub markOrDeleteSnapshot ($$$) {
    # Mark or delete a snapshot, depending on options and errors.
    my $snapshot         = shift; 
    my $sourcesCompleted = shift;
    my $restartable      = shift;

    my $sName = $S{$snapshot}{name};   # for convenience

    my $action; # 'delete' or 'keep'
    if ($restartable and $Opt{'allow-restart'}) {
        # Don't delete if we want to restart
        $action = 'keep';
    } else {
        if ($sourcesCompleted == 0) {
            $action = $Opt{'all-failed'};
            printlog "NOTE: All sources failed, will $action snapshot\n", 1;
        } else {
            $action = $Opt{'some-failed'};
            printlog "NOTE: Some sources failed, will $action snapshot\n", 1;
        }
    }

    if ($Opt{'dry-run'} and ($action eq 'delete')) {
        printlog "Dry run: would have deleted unfinished snapshot '$sName'\n", 1;
        return;
    }

    if ($action eq 'delete') {
        my $rc = deleteSnapshot($snapshot);
        if ($rc == 0) {
            printlog "NOTE: Snapshot deleted\n", 1;
        } else {
            printlog "WARNING: Snapshot could not be deleted (error $rc)\n", 1;
        }
    } else {
        # Mark it as unfinished
        $S{$snapshot}{unfinished} = 1;
    }
}

#---------------------------------------------------------------------------------------------------

sub status ($;\@) {
    # Print a status report on existing snapshots for a single destination
    my $dest   = shift;
    my @cSpecs;
    @cSpecs    = @{(shift)} if @_;

    my $count  = 0; # complete
    my $ucount = 0; # unfinished
    my $total  = 0;
    my $ci = 0;     # index into @cSpecs
    my $pi = -1;    # index into $Opt{pspecs}
    debug "pspecs: " . Dumper($Opt{pspecs}), 'status';

    my $plevel = 1; # only output at this level or above
    my $llevel = 1;
    if ($Opt{status}) {
        # User has asked for status, so make sure it's printed and not logged
        $plevel = 0;
        $llevel = 9;
    }

    printlog under1("\nStatus report for $dest\n"), $plevel,$llevel;
    if (!%S) {
        printlog "No snapshots found\n", $plevel,$llevel;
    } else {
        my $nameLength = max(length($Opt{template}) + 18, 35);
        printlog sprintf("%-*s   %15s   %s\n", $nameLength, 'Snapshot (most recent first)', 
            'Size', @cSpecs ? 'Spec' : ''), $plevel,$llevel;
        printlog sprintf("%-*s   %15s   %s\n", $nameLength, '-'x$nameLength, 
            '-----------', '-------------', @cSpecs ? '--------' : ''), $plevel,$llevel;
        foreach my $s (rsort keys %S) {
            my $name = $S{$s}{name};
            $name .= ' (u)' if $S{$s}{unfinished};
            my $size = $S{$s}{size};
            next if $size < 0;  # skip no-snapshot snapshot
            my $label = '';
            if ($S{$s}{unfinished}) {
                $ucount += 1;
            } else {
                $count += 1;
                if (@cSpecs) {
                    # Find which cSpec applies (complete snapshots only)
                    while ($cSpecs[$ci]->{beg} > $s) { 
                        $ci += 1; 
                    }; 
                    $label = $cSpecs[$ci]->{label};
                    #$label = $cSpecs[$ci]->{rawspec};
                    #$label .= (' ' . $LevelName{$cSpecs[$ci]->{period}});
                }
            }
            # Print period details if we've got to a new spec
            while ($pi < 0 or ${$Opt{pspecs}}[$pi]->{beg} > $s) {
                $pi += 1;
                printlog "Period covered by spec ${$Opt{pspecs}}[$pi]->{rawspec} is " .
                    pair2range(${$Opt{pspecs}}[$pi]->{beg}, ${$Opt{pspecs}}[$pi]->{end}) . "\n", 1;
            }
            printlog sprintf("%-*s   %15s   %s\n", $nameLength, $name, 
                bytes($size) || 'empty', $label), $plevel,$llevel;
            $total += $size;
        }
        printlog sprintf("%-*s   %15s\n", $nameLength, '-'x$nameLength, 
            '-----------'), $plevel,$llevel;
        my $unfinished = '';
        $unfinished = "(+$ucount unfinished)" if $ucount;
        printlog sprintf("%8s %s %-16s %3d   %15s\n", 'Total:', ' 'x($nameLength-30), 
            $unfinished, $count, bytes($total)), $plevel,$llevel;
        if ($count > 0) {
            my $average = floor($total/$count);
            printlog sprintf("%-*s   %15s\n", $nameLength, 'Average:', 
                bytes($average)), $plevel,$llevel;
        }
    }
}

#---------------------------------------------------------------------------------------------------

sub displaySummary () {
    # Print out the results summary
    printlog "\n\nResults Summary\n", 1;
    printlog     "===============\n", 1;
    foreach my $dest (sort keys %Summary) {
        printlog "Destination $dest\n";
        foreach my $source (sort keys %{$Summary{$dest}}) {
            #debug "source=$source DestResult=$DestResult\n", 'dS';
            next if $source eq $DestResult;
            printlog "\tSource $source\n", 1;
            printlog "\t\t$Summary{$dest}{$source}\n", 1;
        }
        printlog "\t$Summary{$dest}{$DestResult}\n", 1;
    }
}

#===================================================================================================

# Periods
# NB periods end on the beginning of the next one

sub getPeriod ($$) {
    # Return the beginning and end of the period containing the moment specified
    # e.g. if the period is 1 (day), return the midnights before and after the moment
    # (If $moment is already the beginning of the period,
    # then stay in that period - don't skip back one)
    # Note: this is for use in calendar mode only
    my $moment  = shift; # as Unix time
    my $periodn = shift;
    my ($beg, $end);
    debug "periodn=$periodn moment=$moment (" . time2stamp($moment) . ")\n", 'gP';
    my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) = localtime($moment);
    given ($periodn) {
        when (0) {
            $beg = timelocal(0,0,$hour,$mday,$mon,$year+1900);
            $end = $beg + HOUR;
        } 
        when (1) {
            $beg = timelocal(0,0,0,$mday,$mon,$year+1900);
            $end = $beg + DAY;
        } 
        when (2) {
            # localtime() returns weekday as 0=Sunday, 6=Saturday
            # $opt{weekstart} is 1..7
            $beg = timelocal(0,0,0,$mday,$mon,$year+1900);  # beginning of today
            my $beg1 = $beg; # for debugging only
            $beg -= (($wday- ($Opt{weekstart}-1) + 7) % 7) * DAY;   # adjust for weekday
            $end = $beg + DAY*7;
            debug "mday=$mday mon=$mon year=$year wday=$wday ws=$Opt{weekstart} beg1=$beg1/" . 
                time2stamp($beg1) . " beg=$beg/" . time2stamp($beg) . 
                " end=$end/" . time2stamp($end) ."\n", 'gP';
        } 
        when (3) {
            $beg = timelocal(0,0,0,1,$mon,$year+1900);
            $end = date2time(DateCalc(time2date($beg), '0:1:0:0:0:0:0')); # +1month
        } 
        when (4) {
            $beg = timelocal(0,0,0,1,0,$year+1900);
            $end = timelocal(0,0,0,1,0,$year+1901);
        } 
        default {
            die "Invalid period number '$periodn' passed to getPeriod";
        }
    }
    return ($beg, $end);
}

#---------------------------------------------------------------------------------------------------

sub prevPeriod ($$) {
    # Given the beginning of one period, 
    # and the period number of the previous one, 
    # return the beginning and end of the previous one.
    # Note: this does not 'align' the period to whole weeks or months etc.,
    # e.g. month before 20110622-234400 is 20110522-234400--20110622-234400
    # This copes with DST and leap years (as far as I can tell).
    my $beg     = shift;
    my $periodn = shift;
    #my $end = $beg;
    my $newbeg;
    if ($periodn == $Level{h}) {
        # When going back an hour, just subtract the relevant number 
        # of seconds.  (If we use DateCalc, it does clever stuff with
        # the change to or from daylight savings, which we don't want.)
        $newbeg = $beg - HOUR;
    } else {
        # For all other periods, use DateCalc, with mode set to 1
        # for 'approximate' mode, e.g. a day is a whole day, and
        # not always 24 hours (if DST starts or ends).
        $newbeg = date2time(DateCalc(time2date($beg), $PrevP[$periodn], 1));
    }
    debug "$LevelName[$periodn] before " . time2stamp($beg) . " starts at " . 
        time2stamp($newbeg) . "\n", 'pP';
    return ($newbeg, $beg);
}

#---------------------------------------------------------------------------------------------------

sub fullPeriod ($$$) {
    # Returns true if the beg,end pair given are a full period
    # Note: this does not 'align' the period to whole weeks or months etc.,
    # e.g. 20110522-234400--20110622-234400 is a full month
    my $beg     = shift;
    my $end     = shift;
    my $periodn = shift;
    my ($beg2, $end2) = prevPeriod($end, $periodn);
    return ($beg == $beg2 && $end == $end2) ? 1 : 0;
}

#---------------------------------------------------------------------------------------------------

sub fmtRange ($$) {
    # Format a timestamp range to make it readable
    my $beg = shift;
    my $end = shift;
    my $range = $end - $beg;
    my $begStamp = time2stamp($beg);
    debug "range is " . pair2range($beg, $end) . "\n", 'fR';
    if ($range == HOUR) {
        return "hour from " . $begStamp;
    } elsif ($range == DAY) {
        return "day from " . $begStamp;
    } elsif ($range == WEEK) {
        return "week from " . $begStamp;
    }
    # DST/leap years: won't get the whole periods we expect,
    # but that's just cosmetic.  TODO check for error from DateCalc
    my $delta = DateCalc(time2date($beg), time2date($end), 1); # (1 = approx mode)
    debug "delta is $delta\n", 'fR';
    if ($delta eq '+0:1:+0:0:0:0:0') {
        return "month from " . $begStamp;
    } elsif ($delta eq '+1:0:+0:0:0:0:0') {
        return "year from " . $begStamp;
    } else {
        return "period " . time2stamp($beg) . " to " . time2stamp($end);
    }
    # TODO expand this for multiple periods? e.g. if $delta =~ /\+(\d+):0:\+0:0:0:0:0/ -> $1 years
}

#---------------------------------------------------------------------------------------------------

sub countPeriods ($$$) {
    # Return the number of periods between two dates
    # (which may not be an integer)
    my $beg     = shift;
    my $end     = shift;
    my $periodn = shift;
    # TODO  Won't return the expected integer result for DST/Leap year cases
    my $delta = DateCalc(time2date($beg), time2date($end));
    my $count = Delta_Format($delta, "%$DMperiod[$periodn]ys");
    return sprintf(($count == floor($count)) ? "%d" : "%.1f", $count);  # 1dp if not an integer
}

#===================================================================================================

sub writeMetadata ($) {
    # Store information from %S in the metadata file via rsync using JSON 
    my $dest = shift;

    return if !%S;

    my $mdfile = "$Opt{template}.status";

    # Create subset of %S to store
    my @S;
    foreach my $s (sort keys %S) {
        push @S,{snapshot => time2stamp($s), unfinished => $S{$s}{unfinished}, size => $S{$s}{size}}
            if $S{$s}{size} >= 0;   # skip no-snapshot snapshot
    }
    my $json = to_json(\@S, {pretty => 1});
    debug "json is '$json'\n", 'wM';

    # Write to a temporary local file
    my ($tempfh, $tempfilename) = tempfile(UNLINK => 1);
    debug "tempfilename is '$tempfilename'", 'wM';
    my $succeeded = 0;
    print $tempfh "# Status of brandysnap backups for '$Opt{template}' on '$dest' as at " . 
        time2stamp(time()) . "\n";
    print $tempfh "# Do not edit or delete this file\n";
    print $tempfh $json;
    close $tempfh;

    # Send the file via rsync
    my @output;
    #my $remPath = $Opt{'remote-rsync-cmd'} ? "--rsync-path '$Opt{'remote-rsync-cmd'}'" : '';
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'}, 
        stdArgs(1) . " --chmod=ug=rw,o=r '$tempfilename' '$dest/$mdfile'", 
        9,9, ); # FIXME \@output);    
    debug "rc=$rc output: '@output'\n", 'wM';
    $succeeded = 1 if !$rc;

    if (!$succeeded) {
        printlog "WARNING: Unable to create metadata file '$mdfile'\n";
        printlog "         Metadata is:\n", 1;
        printlog "$json\n\n", 1;
    }
}

#---------------------------------------------------------------------------------------------------

sub readMetadata ($) {
    # Read the metadata into an array
    my $dest = shift; 

    my $mdArrayRef = (); 
    my $mdfile = "$Opt{template}.status"; 

    # Get the metadata file via rsync to a temporary file
    # (note the hacky way we override the --dry-run option)
    my $tempdir = tempdir(CLEANUP => 1);  
    debug "tempdir is $tempdir\n", 'rM';
    my @output; 
    my $dryrun = $Opt{'dry-run'};
    $Opt{'dry-run'} = 0;
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'}, 
        stdArgs(0) . " '$dest/$mdfile' '$tempdir/'", 
        9,9, \@output);
    $Opt{'dry-run'} = $dryrun;
    debug "rc=$rc output: '@output'\n", 'rM';
    return $mdArrayRef if $rc;

    # Read the metadata file
    if (open MDF, '<', "$tempdir/$mdfile") { 
        my $json = ''; 
        while (<MDF>) { 
            $json .= $_; 
        } 
        close MDF;
        $mdArrayRef = from_json($json, {relaxed => 1});
        foreach my $s (@$mdArrayRef) { 
            # Check that all values are there
            $s->{unfinished} =  0 if !defined $s->{unfinished};
            $s->{size}       = -1 if !defined $s->{size};
            debug "$s->{snapshot} / $s->{unfinished} / $s->{size}\n", 'rM'; 
        } 
    } else { 
        debug "Unable to read temporary metadata file '$tempdir/$mdfile'\n", 'rM';
    }
    return $mdArrayRef; 
}

#===================================================================================================

sub getSnapshotList ($) {
    # Read in the existing snapshots, storing their details in global hash %S.
    # Uses rsync in case the destination is remote.
    my $dest = shift;
    my $template = $Opt{template};  
    debug "dest=$dest template=$Opt{template}\n", 'gSL';
    # Start with an empty list
    %S = ();
    my @listing;
    #my $remPath = '';
    #if (remote($dest) and $Opt{'remote-rsync-cmd'}) {
    #   $remPath = "--rsync-path '$Opt{'remote-rsync-cmd'}'";
    #}
    my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'}, 
        stdArgs(0) . " --list-only '$dest/'", 9,9, \@listing);    
    debug "rc=$rc listing: @listing n=" . scalar @listing ."\n", 'gsL';
    return if $rc;
    my $remote = remote($dest);
    my $daemon = remoteDaemon($dest);
    foreach (@listing) {
        chomp;
        # matching e.g. 'drwxr-xr-x        4096 2011/08/09 23:01:29 library-20110809-165521'
        if (/\d\d:\d\d:\d\d\s+ 
            (?<name>($template)-
            (?<Y>\d{4})(?<M>\d{2})(?<D>\d{2})-
            (?<h>\d{2})(?<m>\d{2})(?<s>\d{2}))
            $/x) {
            # Skip over any template with an invalid date (should not happen)
            next if $+{M} < 1;
            next if $+{M} > 12;
            next if $+{D} < 1;
            next if $+{D} > daysInYearMonth($+{Y}, $+{M}-1);
            next if $+{h} > 23;
            next if $+{m} > 59;
            next if $+{s} > 60;
            # Add information about the snapshot to the global hash
            $S{stamp2time($_)} = {
                name     => "$+{name}",
                path     => $dest,
                fullname => "$dest/$+{name}",
                unfinished => 0,    # unless metadata says otherwise -- see below
                size     => -1,     # no size information yet -- see below
                remote   => $remote,
                daemon   => $daemon,
            };
        } else {
            debug "unmatched: $_\n", 'gSL';
        }
    }
    # Get the metadata
    my $mdArrayRef = readMetadata($dest);
    if ($mdArrayRef) {
        debug "mdArrayRef=$mdArrayRef\n", 'gSL';
        # And combine the two:
        # - warn about entries in metadata for which there is no snapshot
        # - add any unfinished and size information for the existing snapshots
        foreach my $md (@$mdArrayRef) {
            debug "matching md: $md->{snapshot} / $md->{unfinished} / $md->{size}\n", 'gSL';
            my $s = stamp2time($md->{snapshot});
            if ($S{$s}) {
                debug "updating metadata for snapshot $md->{snapshot}\n", 'gSL';
                $S{$s}{unfinished} = $md->{unfinished};  
                $S{$s}{size}       = $md->{size};
            } else {
                printlog "WARNING: snapshot '$template-$md->{snapshot}' is missing\n", 1;
            }
        }
    }
    # Set empty snapshots as unfinished (this shouldn't happen, but 
    # we might as well encourage the system to prune empty snapshots)
    foreach my $s (keys %S) {
        $S{$s}{unfinished} = 1 if $S{$s}{size} == 0;
    }
}

#---------------------------------------------------------------------------------------------------

sub visualise ($$$$$$@) {
    # Draw a pretty picture of the actual and ideal snapshots given pairs of (label, array).
    # Doesn't distinguish good and bad snapshots -- the caller
    # should visualise bad snapshots separately.
    my $beg        = shift;
    my $end        = shift;
    my $showPeriod = shift;
    my $plevel     = shift;
    my $llevel     = shift;
    my $best       = shift; 
    # If plevel/llevel mean there will be no output, just return
    return if $plevel > $Opt{verbose} and $llevel > $Opt{loglevel};
    my $labelwidth = 8;
    #my ($columns, $rows) = Term::Size::chars *STDOUT{IO};
    #$columns = max($columns, 132); # minimum, and also if there's no tty
    my $columns = 80;
    my $size = $columns - $labelwidth - 3;  # - 18; # -18 for log timestamp
    my $scale = ceil(($end - $beg) / $size);    
    debug "beg=$beg end=$end (" . fmtRange($beg,$end) . ") size=$size scale=$scale\n", 'visualise';

    while (@_ > 1) {
        my $label = pack("A$labelwidth", shift);    # force label to specified width
        my @a     = @{(shift)};
        debug "label=$label\n", 'vis';
        my @points = (0) x ($size+1);
        A: foreach my $a (@a) {
            my $pos = floor(($a - $beg) / $scale);
            if ($pos < 0 or $pos > $size) {
                debug "out of range: $pos should be >= 0 and <= $size, a=$a (" . 
                    time2stamp($a) . ")\n", 'vis';
                next A;
            }   
            if ($label eq 'Ideal' and !$points[$pos] and $best == $a) {
                $points[$pos] = 'B';  # this is a nasty hack! what if it later does 'B'+1 ?
            }
            else {
                $points[$pos] += 1;
            }
        }
        my $period = '';
        $period = fmtRange($beg, $end) if $showPeriod;
        printlog "$label|" . join('', 
            map {
                !defined($_) ? '.' : 
                ($_ eq 'B')  ? 'B' :            # 'best' if label is 'Ideal' 
                ($_ < 1)     ? '.' : 
                ($_ > 9)     ? '*' : 
                               $_
            } 
            @points) . "| $period\n", $plevel,$llevel;
    }
}

#---------------------------------------------------------------------------------------------------

sub arrayDiff (\@\@) {
    # Return elements of X that aren't in Y.
    # This doesn't work if there are duplicates in either array!
    # (That's OK, 'cos we're only using it for arrays of snapshot
    # timestamps which are required to be unique.)
    my @X = @{(shift)};
    my @Y = @{(shift)};
    my %Y = map {$_=>1} @Y; # hash version of @Y
    return grep(!defined $Y{$_}, @X);
}

#---------------------------------------------------------------------------------------------------

sub snapshotBefore ($) {
    # Return the snapshot before the one given (if any)
    # For good snapshots, only consider the previous good one.
    # For bad snapshots, any previous one will do.
    my $this = shift;   # the snapshot to compare 
    foreach my $s (rsort keys %S) {
        # skip the bad ones if this one is good:
        next if !$S{$this}{unfinished} and $S{$s}{unfinished}; 
        return $s if $s < $this;
    }
    return 0;   # None found, return 0 which will test as false
}

#---------------------------------------------------------------------------------------------------

sub snapshotAfter ($) {
    # Return the good snapshot after the one given (if any)
    my $this = shift;   # the snapshot to compare 
    my $prevs = 0; # If none found, return 0 which will test as false
    foreach my $s (rsort keys %S) {
        # skip the bad ones if this one is good:
        next if !$S{$this}{unfinished} and $S{$s}{unfinished}; 
        return $prevs if $s <= $this;
        $prevs = $s;
    }
    return $S{$prevs}{unfinished} ? 0 : $prevs; # all snapshots are after the date given, 
                                                # so return the first one (if it's good)
}

#---------------------------------------------------------------------------------------------------

sub snapshotsInPeriod (\@$$;$) {
    # Given a list of snapshot dates in descending order,
    # and a period (beginning and end dates),
    # return the number of snapshots in the period
    # or an array of said snapshots, depending on the last parameter.
    # (Can't use wantarray, because this often gets
    # called in list context even when just the count is needed)
    my @S           = @{(shift)};
    my $beg         = shift;
    my $end         = shift;
    my $returnArray = shift // 0;
    my $count = 0;
    my @SIP;
    foreach my $s (@S) {
        last if $s < $beg;
        if ($s < $end) {
            $count += 1;
            push @SIP, $s;
        }
    }
    debug "$beg..$end " . time2stamp($beg) . '..' . time2stamp($end) . " = $count\n", 'sIP';
    return @SIP if $returnArray;
    return $count;
}

#---------------------------------------------------------------------------------------------------

sub nextOldestSnapshot ($\@) {
    # Return the first snapshot before the given date
    # or 'undef' if there is no such snapshot
    my $time = shift;
    my @S    = @{(shift)};
    my $nextOldest;
    foreach my $s (@S) {
        debug "(" . time2stamp($time) . ") returning " . time2stamp($s) . "\n", 'nOS' if $s < $time;
        return $s if $s < $time;
    }
        debug "(" . time2stamp($time) . ") returning undef\n", 'nOS';
    return undef;
}

#---------------------------------------------------------------------------------------------------

sub getMatchingSnapshots ($$$$\@) {
    # Get a list of snapshots in the given period that
    # are the best match for the ideal/expected snapshots
    # FIXME 26/9/12  Bad snapshots are no longer considered for deletion
    # (see getDeleteList), so this code could be simplified.
    my $beg     = shift;         # beginning of period 
    my $end     = shift;         # end of period
    my $best    = shift;         # seconds from $beg to preferred snapshot time
    my $freqmax = shift;         # maximum number of snapshots in period
    my @S       = @{(shift)};    # array of timestamps of existing snapshots (just those in range)
                                 # in descending order

    my $range  = $end - $beg;   # the length of the period in seconds
    debug "beg=$beg end=$end range=$range best=$best freqmax=$freqmax\n", 'gMS';
    debug " (first S = $S[0]    last S = $S[-1]  -- in descending order)\n", 'gMS' if @S;

    # Special case: if freqmax is zero, delete all snapshots (i.e. keep none)
    if ($freqmax == 0) {
        debug "freqmax==0 -- deleting all\n", 'gMS';
        return ();
    }

    # Special case: if freqmax is UNLIMITED, keep all snapshots
    if ($freqmax == UNLIMITED) {
        debug "freqmax==UNLIMITED -- keeping all\n", 'gMS';
        return @S;
    }

    # Calculate closeness factor for each snapshot
    # from formula closeness=r/(p+n)
    # where p = distance to previous snapshot (or r if none)
    #       n = distance to next snapshot (or n if none)
    #       r = range (see above)
    # possible tuning:
    #      - set p or n to less than r if being on the end gets too much weight
    #       = we're currently trying r/2
    #       - could even make it an option
    my @closeness;
    my $endfactor1 = 0.5;
    my $endfactor2 = 0.5; 
    # Note that only good snapshots are considered when calculating closeness
    # -- this is handled by snapshotBefore/After
    my $totalC = 0;
    for my $s (0..$#S) {
        # Get distance to previous/next good snapshot 
        # (may be beyond current period)
        my $previous = snapshotBefore($S[$s]);
        my $next     = snapshotAfter($S[$s]);
        # If previous/next don't exist, calculate a closeness value
        # that is scaled to $range with a tuning factor
        my $p = $previous ? abs($S[$s] - $previous) : $range * $endfactor1;
        my $n = $next     ? abs($next  - $S[$s])    : $range * $endfactor2;
        # Check if they're infinitely close! 
        # (shouldn't happen because snapshots can't share a date)
        my $closeness = ($p+$n == 0) ? 9999999999 : $range/($p+$n);
        debug "this=" . time2stamp($S[$s]) . " previous=" . time2stamp($previous) . 
            " next=" . time2stamp($next) . " p=$p n=$n c=$closeness\n", 'gMS';
        push @closeness, $closeness;
        $totalC += $closeness;
    }
    my $averageC = $totalC / scalar @S;
    
    my $bestTime = $beg + $best;    # best as Unix time, not offset from $beg

    my $oldBeg = $beg;
    my $oldEnd = $end;

    # 'Shelf-bracket factor' is the size of the extra space
    # between the bracket and the end of the shelf, as
    # a proportion of the distance between brackets:
    #            
    # Shelf:     ====================================
    # Brackets:       \/          \/          \/
    #            <-f1-><----i----->            <-f2->
    # 
    # In terms of snapshots, the shelf represents the range
    # under consideration, and the ideal snapshot times are the brackets.
    #
    # Set each factor to 1/2, assuming the other half will appear
    # in the adjoining period:
    my $sbFactor1 = 0.5;
    my $sbFactor2 = 0.5;

    # Adust beg...end range around a 'close' group of snapshots
    # (i.e. ones that are not near snapshots in previous and next periods)
    # for better spacing of the 'ideals'.
    # A sequence of snapshots is considered a 'group' if either
    # the first or last has a closeness less than 50% (tunable)
    # of the average closesness.
    my $groupFactor = 0.50;     # tuning 
    # (don't forget that @closeness is in descending order by time)
    if ($closeness[0] < ($averageC * $groupFactor)) {
        # Adjust end of range 
        # (+1 to make sure last snapshot is IN the range)
        $end = $S[0];
        debug "gMS: adjusting end of range for closeness, now $end (".time2stamp($end).")\n",'gMS';
        $sbFactor2 = 0;
    }
    if ($closeness[-1] < ($averageC * $groupFactor)) {
        # Adjust beginning of range
        $beg = $S[-1];
        debug "gMS: adjusting beg of range for closeness, now $beg (".time2stamp($beg).")\n",'gMS';
        $sbFactor1 = 0;
    }

    # Further adjustment to weighting of unfinished snapshots
    # as a 'distance' in time -- makes unfinished snapshots always a bad match
    # Factor may need tuning -- currently it's arbitrarily large at 10000 to make 
    # sure that bad snapshots are deleted first.
    my @badoffset;
    foreach my $s (@S) {
        push @badoffset, ($S{$s}{unfinished} ? $range * 10000 : 0);
    }
    debug "badoffsets: @badoffset\n", 'gMS';

    # Make sure best isn't out of range
    # (this is needed sometimes even if the closeness adjustment above isn't used)
    # (the -1 is to make sure $best is IN the range)
    $bestTime = $beg   if $bestTime <  $beg;
    $bestTime = $end-1 if $bestTime >= $end;
    debug "bestTime is now $bestTime (" . time2stamp($bestTime) . ")\n", 'gMS';

    # Recalculate $best as offset into beg..end range
    $best = $bestTime - $beg;
    # and recalculate $range accordingly
    $range = $end - $beg;   # the length of the period in seconds
    debug "best is now $best, range is now $range, best/range is now ".$best/$range."\n", 'gMS';

    # Initially space out the ideal snapshots evenly
    # (these values are used if there is no 'best' time)
    my @I;
    for my $i (0..$freqmax-1) {
        $I[$i] = $beg + ($i+0.5) * $range/$freqmax;
    }

    # Then take into account the h/d/w/m/ybest options
    @I = ();
    # put the best one in first (they get sorted later)
    $I[0] = $bestTime;
    debug "gMS: best=$I[0] " . hms($best) . "\n", 'gMS';
    if ($freqmax > 1) {  # more than one ideal to place, not just the best one
        # Calculate how many should go before and after, then spread them out.
        #my $bbyr = $best / $range;
        my $befores = floor(($best              / $range) * $freqmax);
        my $afters  = floor((($range-1 - $best) / $range) * $freqmax);
        # (the '-1' above is to avoid fence-post errors:
        # we don't want to consider the final second of the period)
        debug "gMS: befores=$befores  afters=$afters\n", 'gMS';
        if ($befores > 0) {
            # befores
            my $interval = floor($best / ($befores + $sbFactor1));
            debug "gMS: befores: interval=$interval " . hms($interval) . "\n", 'gMS';
            my $ptr = $beg + $best;
            for my $i (1..$befores) {
                $ptr -= $interval;
                debug "gMS: B - i=$i ideal = $ptr " . time2stamp($ptr) . "\n", 'gMS'; 
                unshift @I, $ptr;
            }
        }
        if ($afters > 0) {
            # afters
            my $interval = floor(($range-$best) / ($afters + $sbFactor2));
            debug "gMS: afters : interval=$interval " . hms($interval) . "\n", 'gMS';
            my $ptr = $beg + $best;
            for my $i (1..$afters) {
                $ptr += $interval;
                debug "gMS: A - i=$i ideal = $ptr " . time2stamp($ptr) . "\n", 'gMS';
                push @I, $ptr;
            }
        }
    }

    debug "gMS: ideals: @I total: " . @I . "\n", 'gMS';

    # Generate all combinations of the ideal number of snapshots
    # out of the existing snapshots, then calculate a score for each combination
    # and choose the one with the best score
    # (maybe we should leave first and last fixed if they're already aligned?)
    my @combs = combinations(scalar @I, 0..$#S); 
    my $minct = MAXTIME;    # smallest combination total so far
    my $mincti = 0;         # index of smallest combination so far 
    for my $ci (0..$#combs) {
        my @comb = @{$combs[$ci]};
        my $combtotal = 0;
        # Note: ideals are already in ascending order, need to sort the comb
        # (in descending order of indexes, i.e. ascending order of dates)
        @comb = sort {$b <=> $a} @comb;
        foreach my $i (0..$#I) { # for each ideal:
            my $c = $comb[$i];  # choose the corresponding existing snapshot
            # The score for each existing snapshot is:
            #    (distance to ideal * closeness) + offset for being bad
            my $score = abs($I[$i] - $S[$c]) * $closeness[$c] + $badoffset[$c];
            debug "i=$i comb[i]=$c I[i]=$I[$i] S[c]=$S[$c] I-S=". abs($I[$i]-$S[$c]) .
                " clo=$closeness[$c] bo=$badoffset[$c] score=$score\n", 'gMS';
            $combtotal += $score;
        }
        if ($combtotal < $minct) {
            $minct = $combtotal;
            $mincti = $ci;
        }
    }
    debug "" . @combs . " combinations tested\n", 'gMS';
    debug "best combination, with total $minct, is " . 
        join(' ', sort {$b <=> $a} @{$combs[$mincti]}) . "\n", 'gMS';
    my @bestMatches = sort(map {$S[$_]} @{$combs[$mincti]});    # convert indexes back to snapshots 
    debug "best matches: @bestMatches total: " . @bestMatches . "\n", 'gMS';
    debug "closeness (old to new): " . 
        join(' ', (map {sprintf("%.2f", $_)} reverse @closeness)) . 
        ", average: $averageC\n", 'gMS';
    visualise($oldBeg, $oldEnd, 1, 1,1, $bestTime, 
        'Snap'  => \@S, 
        'Ideal' => \@I, 
        'Match' => \@bestMatches) if ($Opt{debug}->{'gMS'});

    return @bestMatches;
}   # getMatchingSnapshots;

#---------------------------------------------------------------------------------------------------

sub bestOffset {
    # Returns the 'best' option for a period as the offset in seconds from 
    # the beginning of the period.
    my $period = shift;
    my $time   = shift; # a time within the period
    my $best;

    return 0 if !$Opt{calendar};    # Silently ignore xbest options in nocalendar mode

    if ($period eq 'h') {
        # Best is a minute from 0-59
        $best = $Opt{hbest} * MINUTE;
    } elsif ($period eq 'd' and $Opt{dbest}) {
        # Best is specified as an hour from 0-23.99
        # so calculate the fraction of day
        $best = $Opt{dbest} * HOUR;
    } elsif ($period eq 'w') {
        # Best is a number from 1 to 7 indicating
        # the day.  Choose the middle of the day (is that OK?)
        # TODO allow e.g. 'Friday 5pm' as supplied by Date::Manip
        $best = ($Opt{wbest}-0.5) * DAY;
    } elsif ($period eq 'm') {
        # Best is the day of the month -- assume noon
        my $mbest = min($Opt{mbest}, daysInMonth($time));
        $best = ($mbest-0.5) * DAY;
    } elsif ($period eq 'y') {
        # Best is the day of the year -- assume noon
        my $ybest = min($Opt{ybest}, daysInYear($time));
        $best = ($ybest-0.5) * DAY;
    } else {
        # Shouldn't happen, but return a usable answer anyway
        $best = 0;  
    }
    return $best;
}

#---------------------------------------------------------------------------------------------------

sub getDeleteList (\@;$) {
    # Using the global hash of existing snapshots,
    # return a list of the ones to be deleted by checking against the list of cSpecs.
    # Plan 
    # - get separate lists of good and bad (unfinished) S's
    # - for each cSpec
    # -   if there are enough good ones, delete all the bad ones
    # -   if more than enough (good+bad), choose as before, but with extra weighting on the bad
    my @cSpecs = @{(shift)};
    my $plevel = shift // 3;    # optional to override print level

    # Separate the complete ('good') from the unfinished ('bad') snapshots
    my @Sgood;
    my @Sbad;
    foreach my $s (rsort keys %S) {
        if ($S{$s}{unfinished}) {
            push @Sbad, $s;
        } else {
            push @Sgood, $s;
        }
    }

    my @allMatches;
    my @D;
    my $firstbeg = MAXTIME; 
    my $lastend = 0;

    debug "\nSgood: " . dumpSnapshots(@Sgood) . "\n", 'gDL';
    debug   " Sbad:  " . dumpSnapshots(@Sbad)  . "\n", 'gDL';
    debug   "no of cspecs = " . @cSpecs . "\n", 'gDL';

    foreach my $c (@cSpecs) {

        my $beg = $c->{beg};
        my $end = $c->{end};
        my $fullend = $c->{fullend};
        debug "\nprocessing cSpec $c->{label} freq=" 
            . fmtFreq($c->{freqmin},$c->{freqmax}) . " range " 
            . pair2range($beg, $end) . " fullend " . time2stamp($c->{fullend}) . "\n", 'gDL';

        # Find candidate snapshots in that period
        # NOTE: $fullend is that same as $end for a full period,
        #       but later than $end for a partial period.
        #       Snapshots in full period are considered when looking for matches
        #       but only those within $beg-$end will actually be deleted
        my @Cgood = snapshotsInPeriod(@Sgood, $beg, $end, 1);
        my @Cbad  = snapshotsInPeriod(@Sbad , $beg, $end, 1);
        my @CgoodFull = snapshotsInPeriod(@Sgood, $beg, $fullend, 1);
        my @CbadFull  = snapshotsInPeriod(@Sbad , $beg, $fullend, 1);
        debug "good candidates: " . dumpSnapshots(@Cgood) . " total: " . @Cgood . "\n", 'gDL';
        debug "bad  candidates: " . dumpSnapshots(@Cbad) . " total: " . @Cbad . "\n", 'gDL';
        if ($fullend != $end) {
            debug "good candidates (full): ".dumpSnapshots(@CgoodFull)." total: ".@CgoodFull."\n", 
               'gDL';
            debug "bad  candidates (full): ".dumpSnapshots(@CbadFull) ." total: ".@CbadFull ."\n", 
               'gDL';
        }   
        if (@Cgood or @Cbad) {
            # there are snapsnots in this period, so adjust the overall range
            $firstbeg = $beg if $beg < $firstbeg;
            $lastend  = $end if $end > $lastend;
        }
        if (!@CgoodFull and !@CbadFull) {
            # no candidates found
            debug "no candidates found to match $c->{label} for period " . 
                time2stamp($beg) . '...' . time2stamp($fullend) . "\n", 'gDL';
        } elsif (@CgoodFull + @CbadFull <= $c->{freqmax}) {
            debug "no more candidates found than required for $c->{label}\n", 'gDL';
            debug "-- allMatches gets " . dumpSnapshots(rsort(@Cgood, @Cbad)) . "\n", 'gDL';
            push @allMatches, rsort (@Cgood, @Cbad);
        } else {
            if (@CgoodFull >= $c->{freqmax}) {
                # enough good candidates: start by deleting all the bad ones
                debug "enough good -- deleting all bad (" . @Cbad. "\)", 'gDL';
                push @D, @Cbad;
                @Cbad = ();
            }
            # FIXME 26/9/12  Maybe we should only delete things if there
            # are enough GOOD ones i.e. just check @Cgood
            # (so bad ones are never deleted unless there enough good ones)
            # NB if this is reinstated, need to consider that CbadFull may not 
            # have been emptied above
#            if (@Cgood + @Cbad > $c->{freqmax}) {
#                # Still more than enough candidates (including bad ones)
#                # - choose the ones to delete (bad ones will get chosen first)
#                my $extras = $c->{freqmax} - (@Cgood + @Cbad);
#                my @Call = rsort(@Cgood, @Cbad);
#                my @I = getMatchingSnapshots($beg, $end, $c->{best}, $c->{freqmax}, @Call); 
#                push @allMatches, @I;
#                push @D, arrayDiff(@Call, @I);
#                debug "storing keepers and deleters:\n\@Call:   " . 
#                    dumpSnapshots(@Call) . "\n\@I:   " . 
#                    dumpSnapshots(@I) . "\ndiff: " .dumpSnapshots(arrayDiff(@Call, @I))."\n",'gDL';
#                debug "\@D is now: " . dumpSnapshots2(@D) . "\n", 'gDL';
            if (@CgoodFull > $c->{freqmax}) {
                # Still more than enough good candidates: choose the ones to delete
                #my $extras = $c->{freqmax} - @Cgood;
                my @I = getMatchingSnapshots($beg, $fullend, $c->{best}, $c->{freqmax},@CgoodFull); 
                my @Ipartial = snapshotsInPeriod(@I, $beg, $end, 1);
                # only keep those within $beg-$end, otherwise there will be duplicates
                push @allMatches, @Ipartial;
                # only delete those within $beg-$end
                push @D, arrayDiff(@Cgood, @Ipartial);
                debug "storing keepers and deleters:" . 
                    "\n\@Cgood:    " . dumpSnapshots(@Cgood) . 
                    "\n\@I:        " . dumpSnapshots(@I) . 
                    "\n\@Ipartial: " . dumpSnapshots(@Ipartial) . 
                     "\ndiff:      " . dumpSnapshots(arrayDiff(@Cgood, @I))."\n", 'gDL';
                debug "\@D is now: " . dumpSnapshots2(@D) . "\n", 'gDL';
            } else {
                debug " no more than required after deleting bad ones\n", 'gDL';
            }
        }

    } # foreach spec

    # sort @D cos may not have been added in order
    @D = rsort @D;

    # Summarise keepers and deleters over whole period
    printlog "\nSummary of all existing snapshots: " . time2stamp($firstbeg) . ' - ' . 
        time2stamp($lastend) . "\n", $plevel; 
    printlog "--------------------------------------------------------------------\n", $plevel;
    printlog((@Sgood+@Sbad) . ' snapshots, ' . @allMatches . ' to be kept, ' . @D . 
            " to be deleted\n", $plevel);
    visualise($firstbeg, $lastend, 0, $plevel,$plevel, 0,   
        'SnapG' => \@Sgood, 
        'SnapB' => \@Sbad, 
        'Keep'  => \@allMatches, 
        'Del'   => \@D);

    return @D;  # list of timestamps of snapshots to delete
}

#===================================================================================================

sub findGoodPeriods ($$$\@) {
    # Starting with period ($beg, $end) and working backwards in time, 
    # check for $count periods each with at least $freqmin snapshots.
    # Return the [beginning, end, good?, snapshots-in-period, period-count] for each period
    # (including the gaps between the 'good' periods).
    # This version keeps periods aligned to calendar days/weeks/months etc.
    # The ($beg, $end) of the last one is then where we're up to.
    my $beg  = shift;
    my $end  = shift;
    my $spec = shift;
    my @S    = @{(shift)};  # just 'good' ones

    my $gpCount = 0;
    my @good;
    my $oldestSnapshot = $S[-1] // MAXTIME;
    debug "range=" . pair2range($beg, $end) . 
        " freqmin=$spec->{freqmin} periodn=$spec->{periodn} " . 
        "count=$spec->{count} oldestS=" . time2stamp($oldestSnapshot) . "\n", 'fGP';

    # Special case:
    # if this is an 'unlimited' spec, then its count will be (effectively) infinite
    # -- we need to keep filling periods until there are no more snapshots
    # -- if any periods have less than freqmin, then relax, just treat them as full
    # This is all dealt with simply by the condition count==UNLIMITED below.

    my $lastPeriodGood = 1;
    while (($end     > $oldestSnapshot) and
           ($gpCount < $spec->{count} )     )  {
        my $sip = snapshotsInPeriod(@S, $beg, $end);
        if (($spec->{count} == UNLIMITED) or 
            ($sip >= $spec->{freqmin}   )    ) {
            push @good, [$beg, $end, 1, $sip, 1];
            $gpCount += 1;    # count it as good
            $lastPeriodGood = 1;
        } else {
            # This period does not have enough snapshots to be considered 'good'
            if (!$lastPeriodGood) {
                # last one was not good either -- combine them
                $good[-1][0]  = $beg;
                $good[-1][3] += $sip;
                $good[-1][4] += 1;
            } else {
                # this is the first bad one
                push @good, [$beg, $end, 0, $sip, 1];
            }
            $lastPeriodGood = 0;
        }
        ($beg, $end) = prevPeriod($beg, $spec->{periodn});
    }
    debug "returning c=$gpCount, " . (join(' ', 
        map {pair2range(@{$_}[0], @{$_}[1]) . "=@{$_}[2]:@{$_}[3]/@{$_}[4]"} @good)) . "\n", 'fGP';
    return ($gpCount, @good);
}

#---------------------------------------------------------------------------------------------------

sub findGoodPeriodsNoCal ($$$\@) {
    # Starting with period ($beg, $end) and working backwards in time, 
    # check for $count periods each with at least $freqmin snapshots.
    # Return the [beginning, end, good?, snapshots-in-period, period-count] for each period
    # (including the gaps between the 'good' periods).
    # This version does not keep periods aligned to calendar days/weeks/months etc.
    # The ($beg, $end) of the last one is then where we're up to.
    my $beg  = shift;
    my $end  = shift;
    my $spec = shift;
    my @S    = @{(shift)};

    my $gpCount = 0;
    my @good;
    my $oldestS = $S[-1] // MAXTIME;
    debug "\nrange=" . pair2range($beg, $end) . 
        " freqmin=$spec->{freqmin} periodn=$spec->{periodn} " . 
        "count=$spec->{count} oldestS=" . time2stamp($oldestS) . "\n", 'fGPNC';

    # Special case:  WHY???   -- I've removed this test now
    # if this is an 'unlimited' spec, then its count will be (effectively) infinite
    # -- we need to keep filling periods until there are no more snapshots
    # -- if any periods have less than freqmin, then relax, just treat them as full
    # This is all dealt with simply by the condition count==UNLIMITED below.

    while (($end > $oldestS) and
        ($gpCount < $spec->{count})   ) {
        debug "looking for $spec->{freqmin} snapshots in one $spec->{period}\n", 'fGPNC';
        my $sip = snapshotsInPeriod(@S, $beg, $end);
        # Look for a contiguous period first
        if (# FIXME why?! ($spec->{count} == UNLIMITED) or 
            ($sip >= $spec->{freqmin}   )    ) {
            push @good, [$beg, $end, 1, $sip, 1];
            $gpCount += 1;    # count it as good
            debug "good one: range=" . pair2range($beg, $end) . " sip=$sip pc=1\n", 'fGPNC';
        } else {
            # No contiguous period --
            # find a group of freqmin snapshots within a period 
            # keeping track of the gap between that group and the last good period
            my $gapend = $end;
            my $skippedSnapshots = 0;
            my $nextS = $end;   # start search where we're up to
            while (($nextS = nextOldestSnapshot($nextS, @S))               and  # yes, '=' not '=='
                   (($beg, $end) = prevPeriod($nextS+1, $spec->{periodn})) and
                   (($sip = snapshotsInPeriod(@S, $beg, $end)) < $spec->{freqmin})) {
                # carry on looking
                $skippedSnapshots += 1;
                debug "skipping nextS=" . time2stamp($nextS) . 
                    ", skipped=$skippedSnapshots\n", 'fGPNC';
            }
            my $pc = countPeriods($end, $gapend, $spec->{periodn});
            if ($nextS) {
                # found a $nextS which marks the end a good period
                push @good, [$end, $gapend, 0, $skippedSnapshots, $pc]; 
                debug "gap: range=" . pair2range($end, $gapend) . 
                    " skippedSnapshots=$skippedSnapshots pc=$pc\n", 'fGPNC';
                push @good, [$beg, $end, 1, $sip, 1];
                debug "good after gap: range=" . pair2range($beg, $end) . 
                    " sip=$sip pc=1\n", 'fGPNC';
                $gpCount += 1;
            } else {
                # ran out of snapshots
                push @good, [$oldestS, $gapend, 0, $skippedSnapshots, $pc];
                debug "gap to oldestS: range=" . pair2range($oldestS, $gapend) . 
                    " skippedSnapshots=$skippedSnapshots pc=$pc\n", 'fGPNC';
            }

        }
        ($beg, $end) = prevPeriod($beg, $spec->{periodn});
    }

    debug "returning c=$gpCount, " . (join(' ', 
        map {pair2range(@{$_}[0], @{$_}[1]) . "=@{$_}[2]:@{$_}[3]/@{$_}[4]"} @good)) . "\n", 
        'fGPNC';
    return ($gpCount, @good);
}

#---------------------------------------------------------------------------------------------------

sub findAllPeriods ($$$\@) {
    # Expand a spec into a list of periods, starting with ($beg, $end).
    # Unlike findGoodPeriods, this doesn't care if the periods
    # have the expected number of snapshots
    my $beg  = shift;
    my $end  = shift;
    my $spec = shift;
    my @S    = @{(shift)};
    return (0, ()) if !@S;
    my @all;
    for my $c (1..$spec->{count}) {
        my $sip = snapshotsInPeriod(@S, $beg, $end);
        # each entry is: [beginning, end, good?, snapshots-in-period, period-count]
        push @all, [$beg, $end, 1, $sip, 1];
        ($beg, $end) = prevPeriod($beg, $spec->{periodn});
    }
    return ($spec->{count}, @all);
}   

#===================================================================================================
# Specs

sub compileSpecs ($$) {  
    # 'Compile' the specs into a list that takes into account
    # the current date and existing snapshots.
    # Works backwards in time from 'now'.
    my $now        = shift;
    my $specsR     = shift; # array ref

    my $oldestS = $now; # Will be the time of the oldest snapshot
    # List good and bad snapshots separately
    my (@Sgood, @Sbad);
    foreach my $s (rsort keys %S) {
        if ($S{$s}{unfinished}) {
            push @Sbad, $s;
        } else {
            push @Sgood, $s;
        }
        $oldestS = $s if $s < $oldestS; # including the bad ones
    }
    debug "now=" . time2stamp($now) . " specsR=$specsR\n", 'cS';

    my $firstSpec = 1;  # first spec applies to the current period
    my @cSpecs; # compile specs

    my $end = $now + 1; # +1 to ensure 'now' is within the current period
    debug "starting with oldestS=" . time2stamp($oldestS) . 
        ", end=" . time2stamp($end) . "\n", 'cS';
    debug "specsR: " . Dumper($specsR), 'cS';

    foreach my $spec (@$specsR) {

        my $beg;
        my $p = 1; # count the periods
        my @goodPeriods;
        my $gpCount = 0;

        if ($Opt{calendar}) {
            # beginning of period containing (end-1)
            ($beg, undef) = getPeriod($end - 1, $spec->{periodn});
        } else {
            # period that ends now
            ($beg, undef) = prevPeriod($end, $spec->{periodn});
        }
        my $sEnd = $end;    # end of all periods covered by $spec 

        debug "[1] spec=$spec->{rawspec} beg,end=" . pair2range($beg,$end) . "\n", 'cS';

        if (!fullPeriod($beg, $end, $spec->{periodn})) { 
            # This is a partial period -- either the current period (if it's the first spec),
            # or because of a change of period
            my $sip = snapshotsInPeriod(@Sgood, $beg, $end);
            if ($firstSpec and $sip >= $spec->{freqmin}) {  
                # This partial period can count as a good one -- do nothing here
                debug "firstSpec and enough S's -- will count as good\n", 'cS';
            } else {
                # Period is partial ...?
                my (undef, $fullend) = getPeriod($beg, $spec->{periodn});
                if ($end <= $oldestS) {
                    debug "not compiling spec $spec->{rawspec} cos it's empty\n", 'cS';
                } else {
                    push @cSpecs, {
                        beg     => $beg, 
                        end     => $end, 
                        freqmin => $spec->{freqmin},
                        # if this is the first period, apply the delete-cp option
                        # by setting freqmax to UNLIMITED so that none will be deleted
                        freqmax => ($firstSpec and !$Opt{'delete-cp'}) ? 
                                        UNLIMITED : $spec->{freqmax},
                        count   => '', # FIXME $spec->{count},
                        period  => $spec->{period},
                        # limit best to partial period:
                        best    => min(bestOffset($spec->{period}, $beg), $end-$beg-1),  
                        rawspec => $spec->{rawspec}, 
                        label   => $spec->{rawspec} . ($firstSpec ? ':current' : ':part'),
                        sgood   => $sip,
                        sbad    => snapshotsInPeriod(@Sbad, $beg, $end),
                        #partial => 1,
                        fullend => $fullend,    # end of period if it weren't partial
                    };  
                    debug "created cSpec [1] " . pair2range($cSpecs[-1]->{beg},$cSpecs[-1]->{end})
                    . " freq=$cSpecs[-1]->{freqmin}-$cSpecs[-1]->{freqmax} " . 
                    "count=$cSpecs[-1]->{count} period=$cSpecs[-1]->{period} best=" . 
                    time2stamp($cSpecs[-1]->{beg}+$cSpecs[-1]->{best}) . 
                    " label=$cSpecs[-1]->{label} " . 
                    "snapshots=$cSpecs[-1]->{sgood}/$cSpecs[-1]->{sbad}\n", 'cS';
                }
                # move to previous complete period
                ($beg, $end) = prevPeriod($beg, $spec->{periodn});   
            }
        }

        debug "[2] spec=$spec->{rawspec} beg,end=" . pair2range($beg,$end) . "\n", 'cS';

        # get list of good periods starting with beg,end
        if ($Opt{safe}) {
            if ($Opt{calendar}) {
                ($gpCount, @goodPeriods) = findGoodPeriods($beg, $end, $spec, @Sgood);
            } else {
                ($gpCount, @goodPeriods) = findGoodPeriodsNoCal($beg, $end, $spec, @Sgood);
            }
        } else {
            # unsafe mode, with or without calendar mode
            ($gpCount, @goodPeriods) = findAllPeriods($beg, $end, $spec, @Sgood);
        }

        foreach my $gpRef (@goodPeriods) {
            ($beg, $end, my $good, my $sip, my $pCount) = @$gpRef;
            if (!$good) { # there's a gap -- either 1 or more incomplete periods, 
                # or (in no-calendar mode) a fractional number of incomplete periods
                push @cSpecs, {
                    beg     => $beg,
                    end     => $end,
                    freqmin => $spec->{freqmin},
                    freqmax => UNLIMITED,    # keep them all  could use freqmax*pCount?
                    period  => $spec->{period},
                    count   => $pCount,   # may be fractional
                    best    => 0,
                    rawspec => $spec->{rawspec}, 
                    label   => $LevelName[$spec->{periodn}] . '(s) with too few snapshots',   
                    sgood   => $sip,
                    sbad    => snapshotsInPeriod(@Sbad, $beg, $end), 
                    #partial => 0,
                    fullend => $end,
                };
                debug "created cSpec [3] " . pair2range($cSpecs[-1]->{beg},$cSpecs[-1]->{end})
                    . " freq=$cSpecs[-1]->{freqmin}-$cSpecs[-1]->{freqmax} " . 
                    "count=$cSpecs[-1]->{count} period=$cSpecs[-1]->{period} best=" . 
                    time2stamp($cSpecs[-1]->{beg}+$cSpecs[-1]->{best}) . 
                    " label=$cSpecs[-1]->{label} " . 
                    "snapshots=$cSpecs[-1]->{sgood}/$cSpecs[-1]->{sbad}\n", 'cS';
            } else { # it's a complete period
                push @cSpecs, {
                    beg     => $beg, 
                    end     => $end, 
                    freqmin => $spec->{freqmin},
                    # if this is the first period, apply the delete-cp option
                    # by setting freqmax to UNLIMITED so that none will be deleted
                    freqmax => (($end == $now+1) and !$Opt{'delete-cp'}) ? 
                                    UNLIMITED : $spec->{freqmax}, 
                    period  => $spec->{period},
                    count   => '', # FIXME $spec->{count},
                    best    => bestOffset($spec->{period}, $beg),
                    rawspec => $spec->{rawspec}, 
                    label   => $spec->{rawspec} . ":$p",
                    sgood   => $sip, 
                    sbad    => snapshotsInPeriod(@Sbad, $beg, $end), 
                    #partial => 0,
                    fullend => $end,
                };  
                debug "created cSpec [4] " . pair2range($cSpecs[-1]->{beg},$cSpecs[-1]->{end}) .
                    " freq=$cSpecs[-1]->{freqmin}-$cSpecs[-1]->{freqmax} " . 
                    "count=$cSpecs[-1]->{count} period=$cSpecs[-1]->{period} best=" . 
                    time2stamp($cSpecs[-1]->{beg}+$cSpecs[-1]->{best}) . 
                    "label=$cSpecs[-1]->{label} " . 
                    "snapshots=$cSpecs[-1]->{sgood}/$cSpecs[-1]->{sbad}\n", 'cS';
                $p += 1;
            }
            # Move back a period beyond @goodPeriods
            ($beg, $end) = prevPeriod($beg, $spec->{periodn}); 
        } # foreach $gpRef

        debug "[3] spec=$spec->{rawspec} beg,end=" . pair2range($beg,$end) . "\n", 'cS';

        # We'll get straight here for specs after the one with not enough good periods
        # (or possibly first time if there aren't any snapshots)
        # NOTE -- we're just accessing the array @goodPeriod.
        # If count is UNLIMITED, potentially unlimited specs -- just do one for documentation.
        my $count = ($spec->{count} == UNLIMITED) ? 1 : $spec->{count};
        debug "any left over? gpCount=$gpCount, count=$count \n", 'cS';
        for my $i (($gpCount+1)..$count) {
            # HERE THERE ARE NO MORE SNAPSHOTS.
            # WE'RE JUST ITERATING THE REMAINING PERIODS OF REMAINING SPECS
            debug "not compiling $spec->{rawspec} count=$i\n", 'cS';
#            push @cSpecs, {
#                beg     => $beg,
#                end     => $end,
#                freqmin => $spec->{freqmin},
#                freqmax => $spec->{freqmax},  # no reason for UNLIMITED here (single period)
#                period  => $spec->{period},
#                count   => $spec->{count},    # or 0,  ?
#                best    => 0,
#                rawspec => $spec->{rawspec}, 
#                label   => $spec->{rawspec} . ":$p empty (2)",
#                sgood   => snapshotsInPeriod(@Sgood, $beg, $end), 
#                    # should always be 0 -- we'll check
#                sbad    => snapshotsInPeriod(@Sbad, $beg, $end), 
#                    # should always be 0 -- we'll check
#                #partial => 0,
#                fullend => $end,
#            };  
            debug "'empty' cSpec is not empty!!!!!!!!!!!!!!!!!!!!\n", 'cS'
                if snapshotsInPeriod(@Sgood, $beg, $end) > 0 ;
            debug "didn't create cSpec [5] " . pair2range($cSpecs[-1]->{beg},$cSpecs[-1]->{end}) . 
                " freq=$cSpecs[-1]->{freqmin}-$cSpecs[-1]->{freqmax} " . 
                "count=$cSpecs[-1]->{count} period=$cSpecs[-1]->{period} best=" . 
                time2stamp($cSpecs[-1]->{beg}+$cSpecs[-1]->{best}) . 
                " label=$cSpecs[-1]->{label} " . 
                "snapshots=$cSpecs[-1]->{sgood}/$cSpecs[-1]->{sbad}\n", 'cS';
            $p += 1;
            ($beg, $end) = prevPeriod($beg, $spec->{periodn});
        }

        # At this point, beg,end is the next period to consider for the CURRENT spec
        # i.e. its end is OK, but its beg is wrong
        # that's fine, we're working back from that end, so
        # we don't even need now = end -- leave end as it is for next time round loop.
        
        # Store details of complete original spec
        $spec->{beg} = $end;
        $spec->{end} = $sEnd;
        debug "status spec=$spec->{rawspec} beg/end " .pair2range($spec->{beg},$spec->{end}), 'cS';

        $firstSpec = 0;

    } # for each spec

    # If there are any snapshots left, they're not needed by any of the specs,
    # and can all be deleted
    if ($end > $oldestS) {
        push @cSpecs, {
            beg     => $oldestS,
            end     => $end,
            freqmin => 0,
            freqmax => 0,     # keep none of them
            period  => '',    # not a proper period -- maps to '' in %LevelName
            count   => '', # FIXME 1,     # there is only 1 of these 'old' specs
            best    => 0,
            rawspec => '',
            label   => 'old',
            sgood   => snapshotsInPeriod(@Sgood, $oldestS, $end), 
            sbad    => snapshotsInPeriod(@Sbad, $oldestS, $end), 
            #partial => 0,
            fullend => $end,
        };  
        debug "created cSpec [6] " . pair2range($cSpecs[-1]->{beg},$cSpecs[-1]->{end}) . 
            " freq=$cSpecs[-1]->{freqmin}-$cSpecs[-1]->{freqmax} " . 
            "count=$cSpecs[-1]->{count} period=$cSpecs[-1]->{period} best=" . 
            time2stamp($cSpecs[-1]->{beg}+$cSpecs[-1]->{best}) . 
            " label=$cSpecs[-1]->{label} snapshots=$cSpecs[-1]->{sgood}/$cSpecs[-1]->{sbad}\n", 
            'cS';
    }

    # Double check that it's ok
    my $lastbeg = $cSpecs[0]->{end};
    foreach my $c (@cSpecs) {
        if ($c->{end} != $lastbeg) {
            debug "skipped " . fmtRange($c->{end}, $lastbeg) . ' with ' . 
                snapshotsInPeriod(@Sgood, $c->{end}, $lastbeg) . " snapshots\n", 'cS';
            debug "!!!!!!!!!!!!! This shouldn't happen any more!\n", 'cS'; 
        }
        if ($c->{beg} > $c->{end}) {
            debug "invalid cSpec -- beg > end: " . fmtRange($c->{beg}, $c->{end}) . "\n", 'cS'; 
            debug "!!!!!!!!!!!!! This shouldn't happen any more!\n", 'cS'; 
        }
        $lastbeg = $c->{beg};
    }

    return @cSpecs;

} # compileSpecs

#---------------------------------------------------------------------------------------------------

sub displayCSpecs (\@;\%) {
    # Display the compiled specs
    my @cSpecs = @{(shift)};
    my $Sref   = shift;   # optional

    printlog "\nCompiled specs:\n", 3;  
    printlog   "---------------\n", 3;

    # e.g.  From              To                F  P      C  Comment              Ss    Ps    Ds
    #       20080101-111122 - 20090101-221111   0  d  99999  6d7.............31 9999  9999  9999
    printlog sprintf("%-15s - %-15s  %5s  %1s  %-37s  %4s  %4s  %4s\n",
        'From', 'To', 'Freq', 'P', 'Spec / Comment', 'Ss', 'Ps', 'Ds'), 3;
    printlog sprintf("%-15s - %-15s  %5s  %1s  %-37s  %4s  %4s  %4s\n",
        '-'x15, '-'x15, '-----', '-', '-'x37, '----', '----', '----'), 3;
    my $totalS = 0;
    my $totalX = 0;
    my $totalD = 0;
    foreach my $c (@cSpecs) { 
        my $excess = max($c->{sgood} + $c->{sbad} - $c->{freqmax}, 0); # how many to delete
        printlog sprintf("%s - %s  %2d-%-2s  %1s  %-37s  %4d  %4d  %4.0d\n",
            time2stamp($c->{beg}), 
            time2stamp($c->{end}),
            $c->{freqmin}, 
            $c->{freqmax} == UNLIMITED ? '*' : $c->{freqmax},    # i.e. all
            $c->{period}, 
            #$c->{count} == UNLIMITED ? 'all' : sprintf("%g", $c->{count}),
            ($c->{count} eq '' ? '' : sprintf("%g ", $c->{count})) . $c->{label}, 
            $c->{sgood},
            $c->{sbad},
            $excess,
        ), 3;
        $totalS += $c->{sgood};
        $totalX += $c->{sbad};
        $totalD += $excess;
    }
    printlog sprintf("%-15s   %-15s  %5s  %1s  %37s  %4s  %4s  %4s\n",
        '', '', '', '', '-'x37, '----', '----', '----'), 3;
    printlog sprintf("%-15s   %-15s  %5s  %1s  %37s  %4d  %4d  %4d\n",
        '', '', '', '', 'Totals:', $totalS, $totalX, $totalD), 3;

    # Visualise each cSpec period
    if ($Sref) {
        my (@Sgood, @Sbad);
        # Create arrays of good and bad snapshots
        foreach my $s (rsort keys %$Sref) {
            push @Sgood, $s if !$$Sref{$s};
            push @Sbad,  $s if $$Sref{$s};
        }
        printlog under1("\nDistribution of existing snapshots within each compiled spec\n"), 3;
        foreach my $c (@cSpecs) {
            my @SiP = snapshotsInPeriod(@Sgood, $c->{beg}, $c->{end}, 1);
            visualise($c->{beg}, $c->{end}, 1, 3,3, 0, $c->{label} => \@SiP);
            if (@Sbad) {
                @SiP = snapshotsInPeriod(@Sbad, $c->{beg}, $c->{end}, 1);
                if (@SiP) {
                    visualise($c->{beg}, $c->{end}, 1, 3,3, 0, ' bad' => \@SiP);
                }
            }
        }
    }

}

#===================================================================================================

sub getExpirableSnapshots ($\@$) {
    # Return a list of snapshots that are 'expirable'
    # because they fall within range of the last 'unlimited' spec,
    # or because the expire-old option allows it
    my $specsR = shift;
    my @cSpecs = @{(shift)};
    my $now    = shift;

    my @S = rsort keys %S;
    debug "\@S=@S specsR=$specsR \$now=$now\n", 'gES';
    my @expirable;
    # The current snapshot and any that we're linking to
    # are never expirable, so shift them off the local copy of @S
    for my $i (0..$Opt{ldcount}) {
        shift @S; 
    }
    if (!@S) {
        debug "no \@S, so no expirables\n", 'gES';
        return @expirable;
    }
    # Put unfinished snapshots on the list first
    foreach my $s (@S) {
        if ($S{$s}{unfinished}) {
            unshift @expirable, $s;
            #delete $S{$s};  FIXME why was that there?
        }
    }
    debug "expiring unfinisheds: " . join(' ', map {time2stamp($_)} @expirable) . "\n", 'gES';
    @S = rsort keys %S;  # now with no unfinished snapshots
    # Return if there are none left
    return @expirable if !@S;
    if ($Opt{'expire-old'}) {
        # Any snapshot is expirable so return @S in oldest-first order.
        push @expirable, reverse @S;
        debug "expire-old: they're all expirable EXCEPT the first one(s): \n", 'gES';
        debug "       " . join(' ', map {time2stamp($_)} @expirable) . "\n", 3;
        return @expirable;
    }
    my $lastSpec = @$specsR[-1];
    if ($lastSpec->{count} == UNLIMITED) {
        my $lastraw = $lastSpec->{rawspec};
        my $i = $#cSpecs;
        # Pick out snapshots in the 'unlimited' cSpec, which is always at the end
        while (($i >= 0) and ($cSpecs[$i]->{label} =~ /^($lastraw)/)) {
            debug "lastC is unlimited, getting expirables\n", 'gES'; 
            unshift @expirable, snapshotsInPeriod(@S, $cSpecs[$i]->{beg}, $cSpecs[$i]->{end}, 1);
            $i -= 1;
        }
    }
    debug "returning expirables: " . join(' ', map {time2stamp($_)} @expirable) . "\n", 'gES';
    return @expirable;
}

#===================================================================================================

sub newSnapshot ($$) {
    # Create a new snapshot stamped with the current time,
    # add the entry to the %S hash, and return its time stamp.
    # Also creates the directory for the snapshot to avoid race conditions.
    my $dest         = shift;
    my $lastSnapshot = shift // 0;   # time of last snapshot (or 0 if none)
    my $snapshot = time();
    
    # Avoid having snapshots in the same period that should be in adjacent periods
    # i.e. small differences in the launch time of brandysnap via cron (for example)
    # could result in one run at, say, 5 seconds after the start of an interval,
    # and the next run at 5 seconds before the end of the same interval.
    # So we introduce a small delay to make sure that doesn't happen
    my $fudgetime = 30; # seconds;
    my $delay = 0;
    my $periodn = ${$Opt{pspecs}}[0]->{periodn}; # period of first spec
    if ($lastSnapshot) {
        if ($Opt{calendar}) {
            # Calendar mode - get beginning and end of current period
            my ($beg, $end) = getPeriod($snapshot, $periodn);
            if ( $lastSnapshot         > $beg       and
                ($lastSnapshot - $beg) < $fudgetime and
                ($end - $snapshot)     < $fudgetime     )  {
                # last snapshot is soon after beginning of period
                # so delay the new one until the beginning of the next period
                $delay = ($end - $snapshot);
            }
            debug "calendar: snapshot=" . time2stamp($snapshot) . 
            " last=" . time2stamp($lastSnapshot) . 
            " periodn=$periodn beg/end=" . pair2range($beg, $end) . 
            " delay=$delay\n", 'nS';
        } else {
            # Non-calendar mode - if the last snapshot was slightly less than a period ago,
            # delay the new one until slightly more than a period since the last one.
            debug "non-calendar\n", 'nS';
            my $sinceLast = $snapshot - $lastSnapshot;
            my $periodSecs = $Hours[$periodn] * HOUR;
            if ($sinceLast < $periodSecs                  and
                $sinceLast > ($periodSecs - 2*$fudgetime)     ) {
                $delay = ($lastSnapshot + $periodSecs) - $snapshot;
            }
            debug "non-calendar: snapshot=" . time2stamp($snapshot) . 
            " last=" . time2stamp($lastSnapshot) . 
            " periodn=$periodn periodSecs=$periodSecs" . 
            " sinceLast=$sinceLast delay=$delay\n", 'nS';
        }
    }
    if ($delay) {
        printlog "\nWaiting $delay seconds so that the new snapshot will be at the beginning of " .
            "the next $LevelName[$periodn]\n", 3;
        sleep $delay;
        $snapshot = time();
        debug "delayed $delay seconds, snapshot time is now " . time2stamp($snapshot) . "\n", 'nS';
    }

    my $name = "$Opt{template}-" . time2stamp($snapshot);
    my $fullname = "$dest/$name";

    # Make sure that a snapshot with that name doesn't already exist
    # and create the directory for it.
    while ($S{$snapshot} or rsyncExists($fullname)) { 
        # Yes, we could just add 1 to $snapshot,
        # but then we might end up in the future...
        sleep 1;
        $snapshot = time();
        $name = "$Opt{template}-" . time2stamp($snapshot);
        $fullname = "$dest/$name";
    }
    my $rc = rsyncMkdir($fullname);
    # if that didn't work, things will go wrong later...
    $S{$snapshot} = {
        name     => $name,
        path     => $dest,
        fullname => $fullname,
        unfinished => 0,
        size     => -1, # not known yet
        remote   => remote($dest),
        daemon   => remoteDaemon($dest),
    };

    return $snapshot;
}

#---------------------------------------------------------------------------------------------------

sub runRsync ($\@$) {
    # Run rsync to create the snapshot.
    # If it fails for lack of disk space, delete expirable snapshots and try again.
    # Returns 1 for successful creation of a new snapshot.
    my $dest               = shift; 
    my @expirableSnapshots = @{(shift)};
    my $snapshot           = shift;
    my @S = rsort keys %S;
    debug "\@eS='@expirableSnapshots' snapshot " . dumpSnapshot($snapshot) . ")\n", 'runRsync';

    my $remoteDest = remote($dest);

    my $succeeded = 1;          # stays true if ALL sources are copied successfully
    my $sourcesSucceeded = 0;   # count of successfully backed-up sources
    my $restartable = 0;        # becomes true if unfinished snapshot should be kept for restarting
    my $lostDest = 0;           # becomes true if we lose connection to destination

    my $deletionsAllSources = 0;

    # Loop through sources one at a time
    SOURCE:
    foreach my $source (@{$Opt{source}}) {

        my $location = $source->{location};
        printlog "\nCopying from source '$location'\n", 1;
        my $remoteSource = remote($location);

        # Override options for this source
        local %Opt = %Opt;
        debug "override? " . Dumper($source), 'rR';
        foreach my $srcOpt (keys %{$source->{options}}) {
            debug "overriding $srcOpt with '" . Dumper($source->{options}->{$srcOpt}) . "'\n", 'rR';
            $Opt{$srcOpt} = $source->{options}->{$srcOpt};
        }

        # Source and destination can not both be remote (rsync limitation)
        if ($remoteDest and $remoteSource) {
            printlog "WARNING: source '$location' and destination '$dest' are both remote\n";
            printlog "         rsync can not do that -- combination has been skipped\n";
            next SOURCE;
        }

        # Set up verbosity option
        my $verbose = '';
        $verbose = '--verbose' if $Opt{verbose} > 2;
        $verbose .= ' --verbose --stats' if $Opt{verbose} > 3;  

        # Add the include/exclude options -- there may be several of each
        # Note that each file/directory is double-quoted to allow escapes to work
        # (see stripQuotes()).
        my $incExcOpts = '';
        foreach my $opt (qw(include include-from exclude exclude-from)) {
            $incExcOpts .= join(' ', map {"--$opt \"$_\""} @{$Opt{$opt}});
        }

        my $destDir = $S{$snapshot}{fullname};    
        my $subDir;
        if ($remoteSource) {
            $subDir = sourceDir($location);
        } else {
            $subDir = hostname;
        }
        $destDir = "$destDir/$subDir";

        # Set up link-dest options if there are any previous snapshots.
        # Loop to create as many link-dests as required.
        # Previous unfinished snapshots are linked in case they contain useful
        # files, but don't count towards the total.
        my $ld = 0; # number of 'good' link-dests created
        my $s = 1;  # start at snapshot 1 (i.e. skip the 0th one that we're creating now)
        my $linkdest = '';
        while ($ld < $Opt{ldcount} and $s <= $#S) {
            debug "linking: s=$s S[s]=$S[$s] S{S[s]}{name}=$S{$S[$s]}{name}\n", 'rR';
            $linkdest .= "--link-dest='../../$S{$S[$s]}{name}/$subDir' ";
            $ld += 1 if !$S{$S[$s]}{unfinished};
            $s += 1;
        }

        # Set compression option
        my $compression = '';
        $compression = '--compress' if $Opt{compress} and ($remoteSource or $remoteDest);

        # Set bandwidth limit, depending on the direction
        # (or none if dest and source are both local)
        my $bwlimit = '';
        $bwlimit = "--bwlimit=$Opt{'bwlimit-in'}"  if $remoteSource;
        $bwlimit = "--bwlimit=$Opt{'bwlimit-out'}" if $remoteDest;

        # Set remote rsync option
        my $rsyncPath = '';
        $rsyncPath = "--rsync-path '$Opt{'remote-rsync-cmd'}'"
            if ($Opt{'remote-rsync-cmd'} and ($remoteSource or $remoteDest));

        printlog "Running rsync from source '$location' to destination '$destDir'\n", 2;
        # Note that rsync-opts is put first -- 
        # our own required options will override the user's
#        my $relative = '--relative';
#        $relative = '' if $Win;     # FIXME random test!
        my $args = "$Opt{'rsync-opts'} $verbose $linkdest $incExcOpts $compression " . 
            "$bwlimit --relative $rsyncPath '$location' '$destDir'";

        my $maxTries = @expirableSnapshots + 1; 
        my $keepTrying = $maxTries;  
        my $deletions = 0;
        my $tries = 0;
        # Run rsync, dealing with various types of error
        while ($keepTrying and !$Interrupted) {
            $tries += 1;
            printlog "Running rsync" . ($maxTries>1 ? " -- try $tries of $maxTries" : "") . "\n", 2;
            my $rsyncInterrupted = '';     # only true of interrupted during the main rsync  
            my ($rc, $rc2) = systemCall($Opt{'rsync-cmd'}, $args, 2,2);
            $rsyncInterrupted = $Interrupted;
            debug "after rsync rc=$rc rc2='$rc2' rsyncInterrupted='$rsyncInterrupted'\n", 1;
            given ($rc) {
                #* success
                when (0) {
                #when ($_ == 0 and (!($location =~ /cache/) or rand()>0.5 ))  # for debugging!
                    debug "success\n", 'rR';
                    $keepTrying = 0;    # no need to try this source again
                    $sourcesSucceeded += 1;
                    $Summary{$dest}{$location} = 'succeeded';
                }
                #* source not found
                when (($_ == 23) and ($rc2 eq 'nofileordir')) {
                    printlog "NOTE: Source '$location' could not be read -- " . 
                        "will continue with any other sources\n", 1;
                    $keepTrying = 0;    # don't try this source again
                    $succeeded = 0;
                    $Summary{$dest}{$location} = 'source not found';
                }
                #* errors that can (usually) be ignored, treat as successful 
                when ([22, 23, 24]) {   
                    # Errors on some files such as problems with permissions,
                    # or files that have vanished -- carry on
                    printlog "NOTE: Some errors from rsync have been ignored\n", 2;
                    $keepTrying = 0;
                    $sourcesSucceeded += 1;
                    $Summary{$dest}{$location} = 'succeeded (minor errors ignored)';
                }
                #* interrupted
                when ($rsyncInterrupted ne '' or $rc2 eq 'interrupted') {
                    printlog "NOTE: rsync was interrupted with signal '$rsyncInterrupted' -- " .
                        "the snapshot will be kept to allow restart\n", 1;
                    $keepTrying = 0;
                    $succeeded = 0;
                    $restartable = 1;
                    $Summary{$dest}{$location} = 'interrupted';
                    last SOURCE;
                }
                #* timed out -- continue with other sources/destinations
                #               which may use a different route
                when (30) {
                    $keepTrying = 0;
                    $succeeded = 0;
                    $restartable = 1;
                    $Summary{$dest}{$location} = 'timed out';
                    if ($remoteDest) {
                        # lost connection to remote destination 
                        printlog 
                            "ERROR: rsync timed out: probably lost connection to destination\n", 1;
                        $lostDest = 1;
                        last SOURCE;
                    } elsif ($remoteSource) {
                        printlog "ERROR: rsync timed out: probably lost connection to source\n", 1;
                    } else {
                        # local/local rsync timed out -- why would that happen?
                        # - perhaps when using NFS, for example.
                        printlog "ERROR: rsync timed out\n", 1;
                    }
                }
                #* errors that can be recovered from -- i.e. lack of space (sometimes)
                when (($_ == 12) and ($rc2 eq 'diskfull') and ($keepTrying > 0)) {
                    # Try to make some space before having another go
                    printlog "WARNING: destination '$dest' is full -- " . 
                        "will delete an old snapshot and try again\n";
                    debug "_=$_ rc=$rc rc2=$rc2 keepTrying=$keepTrying\n", 'rR';
                    my $deleted = deleteOldSnapshots(@expirableSnapshots, 1);
                    if ($deleted >= 1) {
                        # More space freed, so try again
                        printlog "NOTE: Old snapshot deleted successfully -- continuing\n";
                        $deletions += $deleted;
                        $deletionsAllSources += $deleted;
                        $keepTrying -= $deleted;
                        $Summary{$dest}{$location} = 'failed (will try again)';
                    } else {
                        # No snapshots deleted, so stop trying
                        printlog "ERROR: unable to delete old snapshot to make space\n", 1;
                        $keepTrying = 0;
                        $succeeded = 0;
                        $ExitCode = EXITRSYNC;
                        $Summary{$dest}{$location} = 'failed (not enough space)';
                        last SOURCE;
                    }
                }
                #* error that means rename/delete/abandon whole destination
                # 11 e.g. include-from file not found
                # 12 with rc2 = 'nofileordir' i.e. bad destination
                # 5 - failed to start rsyncd protocol
                # 255, various, including wrong ssh password, 
                # -1
                # etc.
                default {
                    printlog "ERROR: rsync reported a serious problem ($rc / '$rc2')\n", 1;
                    $keepTrying = 0;
                    $succeeded = 0;
                    $ExitCode = $rc >= 1000 ? EXITINT : EXITRSYNC;
                    $Summary{$dest}{$location} = "failed (error $rc / '$rc2')";
                    last SOURCE;
                }
            } # given

            debug "No more tries\n", 'rR' if $keepTrying <= 0;
        } # while keep trying for this source

        if ($Cygwin) {
            # Hacky work-around for the fact that /cygdrive/c, aka C:\, 
            # is a special case that gets 000 permissions thanks to Windows' ACL system.
            # This has to be done per-source, because $subDir may be different.
            if ($location =~ m|^/cygdrive/c|) {
                rsyncFixPermsC($destDir, "$verbose $rsyncPath");
            }
        }

        if ($deletions) {
            printlog "NOTE: $deletions expirable snapshots were deleted while " . 
                "processing source '$location'\n", 2;
            printlog "(dry run: no snapshots actually deleted)\n", 2 if $Opt{'dry-run'};
        }

    } # for each source

    if ($succeeded) {
        # Announce success
        printlog "\nrsync successfully created new snapshot '$S{$snapshot}{fullname}'\n", 1;  
        printlog "(dry run: no snapshot actually created)\n", 1 if $Opt{'dry-run'};
        if ($deletionsAllSources) {
            printlog "NOTE: $deletionsAllSources expirable snapshots were deleted" . 
                " from destination '$dest'\n", 2;
            printlog "(dry run: no snapshots actually deleted)\n", 2 if $Opt{'dry-run'};
        }
        $Summary{$dest}{$DestResult} = 'succeeded';
    } else {
        printlog "\nWARNING: rsync did NOT create a complete new snapshot on" . 
            " destination '$dest'\n", 1;
        printlog   "         $sourcesSucceeded out of " . @{$Opt{source}} . 
            " sources were completed\n", 1;
        if ($restartable and $Opt{'allow-restart'}) {
            printlog "NOTE: Unfinished snapshot has been left to allow the run to be restarted\n",1;
            printlog "      (use the --restart option to do so)\n", 1;
            $Summary{$dest}{$DestResult} = 'unfinished (restartable)';
        } else {
            $Summary{$dest}{$DestResult} = "failed -- $sourcesSucceeded sources succeeded";
        }
        markOrDeleteSnapshot($snapshot, $sourcesSucceeded, $restartable);
    }

    return ($succeeded, $lostDest);
}

#---------------------------------------------------------------------------------------------------

sub processDestination ($) {
    # Do the processing for a single destination
    my $dest  = shift;      

    printlog under2("\n\nCreating snapshots on destination $dest\n"), 1;

    # Get list of existing snapshots
    getSnapshotList($dest);
    my @S = rsort keys %S;
    debug "got list of " . @S . " snapshots\n", 'run';

    # If there are no previous snapshots, ignore the 'restart' option
    if ($Opt{restart} and !%S) {
        printlog "NOTE: No previous snapshots found, so 'restart' option will be ignored\n", 1;
        $Opt{restart} = 0;  # don't worry, this copy of %Opt is local to this destination
    }

    my $now = time();   # Now, and/or timestamp of new snapshot
    if ($Opt{snapshot}) {
        # Check when the last successful snapshot was run
        if ($Opt{'min-interval'} > 0 and @S > 1) {
            # If the last snapshot was unsuccessful and within min-interval, just restart it
            if ($Opt{'allow-restart'} and 
                $S{$S[0]}{unfinished} and 
                ($now - $S[0]) < ($Opt{'min-interval'} * MINUTE)) {
                if (!$Opt{restart}) {
                    printlog "\nNOTE: Automatically restarting recent unsuccessful snapshot", 1;
                    $Opt{restart} = 1;
                }
            } else {
                # No auto-restart -- don't run if successful recently
                my $s = 0;
                while ($s < @S and $S{$S[$s]}{unfinished}) {
                    $s += 1;
                }
                if ($s < @S and ($now - $S[$s]) < ($Opt{'min-interval'} * MINUTE)) {
                    printlog "\nNOTE: It has been less than $Opt{'min-interval'} minutes\n" . 
                    "      since the last successful snapshot: skipping this destination\n", 1;
                    return;
                }
            }
        }
        if ($Opt{restart}) {
            # Restarting the previous snapshot (whether complete or not)
            $now = $S[0];
            debug "restarting with now=$now: " . dumpSnapshot($now) . "\n", 'pD';
            # remove the unfinished flag -- hopefully, we'll complete it
            $S{$now}{unfinished} = 0;
            printlog under1("\nRestarting snapshot $S{$now}{name}\n"), 1;
        } else {
            # Normal run, creating a new snapshot.
            # Create the new entry in %S, and get the new time stamp
            $now = newSnapshot($dest, $S[0]);
            # and include it in the list 
            unshift @S, $now;
            printlog under1("\nCreating snapshot $S{$now}{name}\n"), 1;
        }
    }

    # 'Compile' the specs taking account of current time and existing snapshots
    my @cSpecs;
    @cSpecs = compileSpecs($now, $Opt{pspecs});
    displayCSpecs(@cSpecs);
    
    # Get the list of snapshots to be deleted
    my @D;
    @D = getDeleteList(@cSpecs) if $Opt{delete};
    # deleteNew is true if the new snapshot would be one of those to be deleted:
    my $deleteNew = grep {$_ == $now} @D;
    debug "deleteNew=$deleteNew", 'pD';

    if ($Opt{snapshot} or $Opt{delete}) {
        # Set the destination lock, and give up if that failed
        return if !rsyncLockDest($dest);
    }
    my $lostDest = 0;
    if ($Opt{snapshot}) {                                         # FIXME why? -v
        if ($deleteNew and $Opt{delete} and $Opt{'delete-cp'}) { #  and !$Opt{'allow-restart'}) {
            printlog "NOTE: The new snapshot would be immediately deleted,\n" . 
                     "      so rsync will NOT be run for this destination\n", 1;
        } else {
            my @expirableSnapshots = getExpirableSnapshots($Opt{pspecs}, @cSpecs, $now);
            # Run rsync to create the new snapshot
            (my $rsyncOK, $lostDest) = runRsync($dest, @expirableSnapshots, $now);  
        }
    } else {
        printlog "NOTE: Option 'no-snapshot' given, so no new snapshot will be created\n", 2;
    }

    if (!$Interrupted) {
        if ($Opt{delete}) {
            # Get the delete list again because the snapshot we've just created (if any)
            # may not be complete
            @D = getDeleteList(@cSpecs) if $Opt{snapshot};
            # Now we can actually delete things
            deleteSnapshots(@D, @cSpecs, $deleteNew);
        } else {
            printlog "NOTE: Option 'no-delete' given, so no existing snapshots will be deleted\n",2;
        }
    }

    if (!$lostDest and ($Opt{snapshot} or $Opt{delete})) {
        # Complete and store the metadata
        updateSnapshotSizes();
        writeMetadata($dest);
        # Clear the destination lock
        rsyncUnlockDest($dest);
    }
    # Display the current situation after recompiling the specs to allow for changes
    @cSpecs = compileSpecs($now, $Opt{pspecs});  
    status($dest, @cSpecs);

}

#---------------------------------------------------------------------------------------------------
    
sub systemCall ($$$$;$$) {
    # Run a system command, given as a program name and argument string.
    # Return the command's return code and extra info if possible.
    # Elaborate scheme for catching STDOUT and STDERR taken from the PERL FAQ 
    # http://perldoc.perl.org/perlfaq8.html#How-can-I-capture-STDERR-from-an-external-command?
    my $cmd         = shift;
    my $args        = shift;    # as a string
    my $stdoutLevel = shift;
    my $stderrLevel = shift;
    my $stdoutRef   = shift;    # if specified, it's an array reference that will receive lines 
                                # from stdout OR a file handle to store stdout lines in
    my $stderrRef   = shift;    # ditto for stderr
    my $rc = 0;
    my $rc2 = '';
    if ($Opt{'dry-run'}) {
        if ($cmd eq $Opt{'rsync-cmd'}) {
            # Special case: if cmd is 'rsync', pass dry-run to rsync and really run it
            # (added at the beginning because of source and dest)
            $args = '--dry-run ' . $args;
        } else {
            printlog "Dry-run: would have run '$cmd $args'\n", 1;
            return 0;
        }
    }
    debug "cmd=|$cmd| args=|$args|\n", 'sC';
    printlog "Running command: $cmd $args\n", $stdoutLevel;
    {
        local *CATCHERR = IO::File->new_tmpfile;
        my $pid = open3(gensym, \*CATCHOUT, ">&CATCHERR", "$cmd $args");
        # Deal with stdout from $cmd
        my $outputStarted = 0;
        while (<CATCHOUT>) {
            if (!$outputStarted) {
                printlog "------- $cmd output starts\n", $stdoutLevel unless $stdoutRef;
                $outputStarted = 1;
            }
            if (ref($stdoutRef) eq 'ARRAY') {
                push @$stdoutRef, $_; 
            } elsif (ref($stdoutRef) eq 'GLOB') {
                print $stdoutRef $_;
            } else {
                printlog $_, $stdoutLevel;
            }
        }
        if ($outputStarted) {
            printlog "------- $cmd output ends\n", $stdoutLevel unless $stdoutRef;
        }
        waitpid($pid, 0);
        $rc = $?;
        $rc2 = '';
        # Deal with stderr from $cmd
        seek CATCHERR, 0, 0;
        $outputStarted = 0;
        while (<CATCHERR>) {
            if (!$outputStarted) {
                printlog "------- $cmd errors start\n", $stderrLevel unless $stderrRef;
                $outputStarted = 1;
            }
            # scan for specific errors
            if (/No space left on device/i) {    # (this text is not always there)
                $rc2 = 'diskfull';
            } elsif (/No such file or directory/i) {
                $rc2 = 'nofileordir';
            } elsif (/received SIGINT/i) {
                $rc2 = 'interrupted';
            }
            if (ref($stderrRef) eq 'ARRAY') {
                push @$stderrRef, $_; 
            } elsif (ref($stderrRef) eq 'GLOB') {
                print $stderrRef $_;
            } else {
                printlog $_, $stderrLevel;
            }
        }
        if ($outputStarted) {
            printlog "------- $cmd errors end\n", $stderrLevel unless $stderrRef;
        }
    }
    if ($rc == -1) {
        debug "Failed to execute command '$cmd $args': $!\n", 'sC';
    } elsif ($rc & 127) {
        my $sig = $rc & 127;
        debug "Command '$cmd $args' died with signal $sig\n", 'sC';
        #  ($? & 127),  ($? & 128) ? 'with' : 'without';
        $rc = ($rc & 127) + 1000;
    } else {
        $rc = $rc >> 8;
        debug "Command '$cmd $args' exited with value $rc\n", 'sC' if $rc;
    }
    return ($rc, $rc2);
}

#===================================================================================================

# Test suite
# Code here can be called by running the script with the '--test n' option.
# This is for developers (i.e. me) only!

sub testSuite { 

    printlog "\nRunning test $Opt{test}\n";
    printlog   "===============\n";

    # Default options for testing
    $Opt{template} = "test$Opt{test}";
    # FIXME change source and dest to use contexts
    ${$Opt{source}}[0]      = '~/tmp/bstestsource';
    ${$Opt{destination}}[0] = '~/tmp/bstestdest';
    $Opt{verbose}  = 3;
    state $template = $Opt{template};   # 'state' to avoid sharing warning
    state $dest = ${$Opt{destination}}[0];
    state $now = stamp2time('20110529-230000');

    sub mks ($) {
        # Generate a snapshot entry for %S, given its time stamp
        # and variables local to testSuite
        my $stamp = shift;
        return (stamp2time($stamp) => 
            {name=>'$template-$stamp', path=>$dest, 
                fullname=>"$dest/$template-$stamp", 
                unfinished=>0, size=>-1, remote=>0, daemon=>0});
    }

    given ($Opt{test}) {

        when (1) {
            # parsing specs
            my @specs = (
            '4d2,1d,2w3,1m6',
            '4d2,1d,1m6',
            '2h6,1h,1d,1w',
            '1d7,1d,1w2',   # zero padding
            '1d,1m1',       # jump from day to month
            '1d7,2h3,1d,1w1',
            '0-1d,3-7w,4-4m1',
            '2h3,1d,1w1',  # problem -- doesn't line up hours before lining up days
            '2h3,1w,1m1',  # likewise...
            '1h1,1d1,1h4,1w1,1d1,1h19,1d,1w1', # silly but valid
            '0-0d1',
            '4-3w1',
            '1d1,1w0',
            '1d1,0w1',
            '1d1,1w',
            '6d7,7w1,2w2,4m5,1m6,2y',
            'd1,1q3,abc,-1d1,1.3w2.9, 1d2 ', # check for invalid ones
            );
            foreach my $spec (@specs) {
                print "\nparsing $spec...\n";
                my ($w, $e, @ps) = parseSpecs($spec);
                print "...gives w=$w e=$e " . (join(',', map {"$_->{rawspec}"} @ps)) . "\n";
            }
        }

        # %S needs to look like this:
        # Hash of snapshots, keyed by snapshot Unix time,
        # each entry containing these fields:
        #  name     - name of the snapshot directory e.g. 'docs-20111130-320234'
        #  path     - directory path to name (NOT ending with '/')
        #  fullname - same as path/name   
        #  unfinished - true if it's an unfinished snapshot, 
        #  remote   - true if it's on a remote destination
        #  daemon   - true if it's remote and accessed via an rsync daemon
        # Which makes it all very tedious.
        when (2) {
            # 2
            my ($warnings, $errors, @specs) = parseSpecs('3d1');
            %S = (
                mks('20110508-090000'),
                mks('20110508-100000'),
                mks('20110508-120000'),
                mks('20110508-180000'),
            );
            my @cSpecs = compileSpecs($now, @specs);
            displayCSpecs(@cSpecs);
        }

        # FIXME the rest of the tests don't work anymore because of the 
        # changes to %S -- maybe we'll abandon them.
        # TODO unless we add a sub to generate %S entries.
        when (3) {
            # 3 
            my ($warnings, $errors, @specs) = parseSpecs('3d1');
            %S = (
                mks('20110508-030000'),
                mks('20110508-060000'),
                mks('20110508-100000'),
                mks('20110508-150000'),
                mks('20110508-180000'),
            );
            my @cSpecs = compileSpecs($now, @specs);
            displayCSpecs(@cSpecs);
        }

        when (4) {
            # 4 - good test for combinations etc:
            my ($warnings, $errors, @specs) = parseSpecs('6d1');
            %S = (
                mks('20110508-010000'),
                mks('20110508-011000'),
                mks('20110508-012000'),
                mks('20110508-013000'),
                mks('20110508-090000'),
                mks('20110508-120000'),
                mks('20110508-150000'),
                mks('20110508-180000'),
                mks('20110508-203000'),
                mks('20110508-210000'),
            );
            my @cSpecs = compileSpecs($now, @specs);
            displayCSpecs(@cSpecs);
        }

        when (5) {
            # 5 - more than one day's worth
            my ($warnings, $errors, @specs) = parseSpecs('8d1,7d3,1d4');
            %S = (
                mks('20110506-010000'),
                mks('20110506-011000'),
                mks('20110506-012000'),
                mks('20110507-013000'),
                mks('20110507-090000'),
                mks('20110507-120000'),
                mks('20110508-150000'),
                mks('20110508-180000'),
                mks('20110508-203000'),
                mks('20110508-210000'),
            );
            my @cSpecs = compileSpecs($now, @specs);
            displayCSpecs(@cSpecs);
            # FIXED this doesn't give ideal result -- chooses B's that are too close together
            # FIXED: 1d1 -> middle one -- because of initial spacing
            #        2d1 -> first and one near the end -- 
            #           why? because of initial spacing, so that's OK
        }

        when (6) {
            # Test 6 -- example of bad choice with close snapshots:  
            my ($warnings, $errors, @specs) = parseSpecs('3w1');
            %S = (
                mks('20110426-050000'),
                mks('20110427-050000'),
                mks('20110428-050000'),
                mks('20110428-060000'),
            );
            my @cSpecs = compileSpecs($now, @specs);
            displayCSpecs(@cSpecs);
        }

        when (7) {
            # Test 5 - testing goodPeriods
            my ($warnings, $errors, @specs) = parseSpecs('6d1,2d4,3w1');  
                # note total of 5d -- not run at w/e
            %S = (
                mks('20110506-010000'),
                mks('20110506-020000'),
                mks('20110506-030000'),
                mks('20110506-040000'),
                mks('20110506-044000'),
                mks('20110506-050000'),
                mks('20110505-090000'),
                mks('20110505-140000'),
                mks('20110505-180000'),
                mks('20110504-090000'),
                mks('20110504-140000'),
                mks('20110504-180000'),
                mks('20110503-090000'),
                mks('20110503-140000'),
                mks('20110503-180000'),
                mks('20110501-090000'),
                mks('20110501-140000'),
                mks('20110501-180000'),
                mks('20110428-050000'),
                mks('20110427-050000'),
                mks('20110426-060000'),
                mks('20110426-040000'),
                #   mks('20100428-0500'),
            )   ;
            my @cSpecs = compileSpecs($now, @specs);
            displayCSpecs(@cSpecs);
        }

        when (8) {
            # Test 8 - wonky spec
            my ($warnings, $errors, @specs) = parseSpecs('2d7,3w1,1d1,2m1'); # 2d7,3w1,1d1,2m1
            %S = (
                mks('20110529-090000'), mks('20110529-120000'), mks('20110529-180000'),  # current
                mks('20110528-090000'), mks('20110528-120000'), mks('20110528-180000'),  # 2d7...
                mks('20110527-090000'), mks('20110527-120000'), mks('20110527-180000'),
                mks('20110526-090000'), mks('20110526-120000'), mks('20110526-180000'),
                mks('20110525-090000'), mks('20110525-120000'), mks('20110525-180000'),
                mks('20110524-090000'), mks('20110524-120000'), mks('20110524-180000'),
                mks('20110523-090000'), mks('20110523-120000'), mks('20110523-180000'),
                mks('20110522-090000'), mks('20110522-120000'), mks('20110522-180000'),  
                mks('20110521-090000'), mks('20110521-120000'), mks('20110521-180000'),  # 3w1...
                mks('20110518-090000'), mks('20110518-120000'), mks('20110518-180000'),
                mks('20110515-090000'), mks('20110515-120000'), mks('20110515-180000'),  # 1d1
                mks('20110512-090000'), mks('20110512-090100'), mks('20110512-090200'),  
                mks('20110512-090300'), mks('20110512-090400'), mks('20110512-090400'),  
                mks('20110512-090600'), mks('20110512-090700'), mks('20110512-090800'),  
                mks('20110512-090900'), mks('20110512-091000'), mks('20110512-091100'),  
                mks('20110403-090000'), mks('20110403-120000'), mks('20110403-180000'),  # 4m1
                mks('20110301-125900'),
            );
            my $now = stamp2time('20110529-230000');
            debug "\nTest run on $now " . time2stamp($now) . "\n";
            my @cSpecs;
            debug "\nsafely, calendar:\n";
            $Opt{calendar} = 1;
            $Opt{safe} = 1;
            @cSpecs = compileSpecs($now, @specs);
            displayCSpecs(@cSpecs);
            debug "\nand now unsafely, calendar:\n";
            $Opt{calendar} = 1;
            $Opt{safe} = 0;
            @cSpecs = compileSpecs($now, @specs);
            displayCSpecs(@cSpecs);
            debug "\nand now safely, non-calendar:\n";
            $Opt{calendar} = 0;
            $Opt{safe} = 1;
            @cSpecs = compileSpecs($now, @specs);
            displayCSpecs(@cSpecs);
            debug "\nand now unsafely, non-calendar:\n";
            $Opt{calendar} = 0;
            $Opt{safe} = 0;
            @cSpecs = compileSpecs($now, @specs);
            displayCSpecs(@cSpecs);
            #my @D = getDeleteList(@cSpecs);
        }

        when (9) {
            sub genTests {
                my (undef, undef, @specs) = parseSpecs('6d1,2d4,3w3,8m1'); 
                    # note total of 5d -- not run at w/e
                # need enough snapshots to fill all specs and then some
                for (my $date = stamp2time('20110401-070000'); 
                        $date < stamp2time('20110501-000000'); 
                        $date += HOUR*3) {
                    #for (my $date = stamp2time('20110401-070000'); 
                    #        $date < stamp2time('20110402-000000'); 
                    #        $date += HOUR*3) {
                    printlog "\n" . '-' x 80;
                    printlog "\nTest run on $date " . time2stamp($date) . "\n";
                    $S{$date} = {
                        name => time2stamp($date), 
                        path => "${$Opt{destination}}[0]",
                        fullname => "${$Opt{destination}}[0]/" . time2stamp($date),
                        unfinished => 0, 
                        remote => remote("${$Opt{destination}}[0]"),
                        daemon => remoteDaemon("${$Opt{destination}}[0]"),
                    };  # make snapshot
                    my @cSpecs = compileSpecs($date+HOUR/2, @specs);
                    displayCSpecs(@cSpecs);
                    my @D = getDeleteList(@cSpecs);
                    #@S = arrayDiff(%S, @D); # 'delete' them
                    foreach my $d (@D) {
                        delete $S{$d};
                    }
                }   
            }
            genTests;
        }

        when (10) {
            # 24 -> 4
            # better matching: don't favour ends if evenly spaced throughout the day
            #   -- somehow measure evenness, and then reduce endness
            $Opt{logfile} = './bstest10.log';
            $Opt{dbest} = 17;
            my (undef, undef, @specs) = parseSpecs('1h24,4d6');
            %S = (
                mks('20110530-003000'), mks('20110530-013000'), mks('20110530-023000'),  
                mks('20110530-033000'), mks('20110530-043000'), mks('20110530-053000'),  
                mks('20110530-063000'), mks('20110530-073000'), mks('20110530-083000'),  
                mks('20110530-093000'), mks('20110530-103000'), mks('20110530-113000'),  
                mks('20110530-123000'), mks('20110530-133000'), mks('20110530-143000'),  
                mks('20110530-153000'), mks('20110530-163000'), mks('20110530-173000'),  
                mks('20110530-183000'), mks('20110530-193000'), mks('20110530-203000'),  
                mks('20110530-213000'), mks('20110530-223000'), mks('20110530-233000'),  

                mks('20110529-003000'), mks('20110529-013000'), mks('20110529-023000'),  
                mks('20110529-033000'), mks('20110529-043000'), mks('20110529-053000'),  
                mks('20110529-063000'), mks('20110529-073000'), mks('20110529-083000'),  
                mks('20110529-093000'), mks('20110529-103000'), mks('20110529-113000'),  
                mks('20110529-123000'), mks('20110529-133000'), mks('20110529-143000'),  
                mks('20110529-153000'), mks('20110529-163000'), mks('20110529-173000'),  
                mks('20110529-183000'), mks('20110529-193000'), mks('20110529-203000'),  
                mks('20110529-213000'), mks('20110529-223000'), mks('20110529-233000'),  

                mks('20110528-213000'), mks('20110528-223000'), mks('20110528-233000'),  
            );
            $Opt{debug}->{gMS} = 1;
            my $now = stamp2time('20110531-003000');
            my @cSpecs = compileSpecs $now, \@specs;
            displayCSpecs(@cSpecs);
            my @D = getDeleteList(@cSpecs);
        }

        when (11) {
            # testing 'grouping'
            $Opt{logfile} = './bstest11.log';
            my (undef, undef, @specs) = parseSpecs('1h9,4d6');
            %S = (
                mks('20110530-093000'), mks('20110530-103000'), mks('20110530-113000'),  
                mks('20110530-123000'), mks('20110530-133000'), mks('20110530-143000'),  
                mks('20110530-153000'), mks('20110530-163000'), mks('20110530-173000'),  

                mks('20110529-093000'), mks('20110529-103000'), mks('20110529-113000'),  
                mks('20110529-123000'), mks('20110529-133000'), mks('20110529-143000'),  
                mks('20110529-153000'), mks('20110529-163000'), mks('20110529-173000'),  

                mks('20110528-213000'), mks('20110528-223000'), mks('20110528-233000'),  
            )
            ;
            my $now = stamp2time('20110531-003000');
            my @cSpecs = compileSpecs($now, \@specs);
            displayCSpecs(@cSpecs);
            my @D = getDeleteList(@cSpecs);
        }

        when (12) {
            # testing expirables  
            $Opt{source} = ['/home/chris/Dropbox/perlwork', '/library/pictures/Fujical.jpg'];
            $Opt{destination} = '/home/chris/tmp/bstest12/';
            $Opt{template} = 'test12';
            $Opt{spec} = '2d';
            $Opt{debug} = 3;
            $Opt{verbose} = 2;
            $Opt{loglevel} = 3;
            $Opt{logfile} = '/home/chris/Dropbox/perlwork/brandysnap/bstest12.log';
            $Opt{test} = 12;
            $Opt{exclude}='*.log*';
            #$Opt{delete-cp} = 0;
            #$Opt{dry-run} = 0;
            ##$Opt{delete} = 1;
            #
            %S = (
                mks('20110530-093000'), mks('20110530-103000'), mks('20110530-113000'),  
                mks('20110529-123000'), mks('20110529-133000'), mks('20110529-143000'),  
                mks('20110528-153000'), mks('20110528-163000'), mks('20110528-173000'),  

                mks('20110527-093000'), mks('20110527-103000'), mks('20110527-113000'),  
                mks('20110526-123000'), mks('20110526-133000'), mks('20110526-143000'),  
                mks('20110525-153000'), mks('20110525-163000'), mks('20110525-173000'),  

                mks('20110524-213000'), mks('20110524-223000'), mks('20110524-233000'),  
            )
            ;
            my $dest = ${$Opt{destination}}[0];
            # generate initial state
            foreach my $s (keys %S) {
                #    mkdir "$dest/" . snapshotName($s, 0);  # FIXME no more snapshotName
            }
            my $now = stamp2time('20110531-003000');
            my ($warnings, $errors, @specs) = parseSpecs('2d'); 
            my @cSpecs = compileSpecs($now, \@specs);
            displayCSpecs(@cSpecs);
            my @D = getDeleteList(@cSpecs);
            #processDestination($dest, @specs);
        }

        when (13) {
            # low-level tests of date routines
            my ($beg, $end) = prevPeriod(stamp2time('20110622-234400'), 1);    # 1=day
            fullPeriod($beg, $end, 3);
            ($beg, $end) = prevPeriod(stamp2time('20110622-234400'), 3);    # 3=month
            fullPeriod($beg, $end, 3);
            ($beg, $end) = getPeriod(stamp2time('20110622-234400'), 2);    
            fullPeriod($beg, $end, 3);
            fullPeriod($beg, $end, 2);
        }

        when (14) {
            debug "Testing findGoodPeriods\n";
            $Opt{spec} = '1d7,1w3,1m11,1y';
            $Opt{verbose} = 3;
            $Opt{loglevel} = 3;
            $Opt{debug} = 5;
            $Opt{calendar} = 1;
            #
            %S = rsort(
                # just one will do
                mks('20110627-093000'),
            )
            ;
            my $dest = ${$Opt{destination}}[0];
            my $now = stamp2time('20110627-133000');
            my ($warnings, $errors, @specs) = parseSpecs($Opt{spec}); 
            my @cSpecs = compileSpecs($now, \@specs);
            displayCSpecs(@cSpecs);
        }

        when (15) {
            debug "Testing findGoodPeriodsNoCal\n";
            # testing expirables  
            $Opt{spec} = '3d2,3w1,99m1';
            $Opt{verbose} = 2;
            $Opt{loglevel} = 3;
            $Opt{calendar} = 0;
            #
            %S = (
                # a good day, then space, then a good day
                mks('20110627-093000'), mks('20110627-103000'), mks('20110627-113000'),  
                mks('20110626-123000'), mks('20110625-133000'), mks('20110624-143000'),  
                mks('20110623-153000'), mks('20110623-163000'), mks('20110623-173000'),
                # then space, then a good week
                mks('20110604-143000'), mks('20110604-133000'), mks('20110604-123000'),
                # then space forever...
                mks('20110524-213000'), mks('20110424-223000'), mks('20110324-233000'),
            )
            ;
            my $dest = ${$Opt{destination}}[0];
            my $now = stamp2time('20110627-133000');
            my ($warnings, $errors, @specs) = parseSpecs($Opt{spec}); 
            my @cSpecs = compileSpecs($now, \@specs);
            displayCSpecs(@cSpecs);
        }

        when (16) {
            print "Testing markOrDeleteSnapshot\n";
            $Opt{template} = 'test1';
            $Opt{destination} = '/home/chris/tmp';
            #markOrDeleteSnapshot("/home/chris/tmp", "test1-20110716-164234", '');
            #markOrDeleteSnapshot('chris@localhost:/home/chris/tmp', "test1-20110809-170104", '');
            %S = getSnapshotList("/home/chris/tmp");
            print "%S: ";
            foreach my $s (rsort keys %S) {
                print "$s: $S{$s}{unfinished} ";
            }
            print "\n";
            status('/home/chris/tmp');
        }

        when (17) {
            print "Testing min/max frequencies\n";
            $Opt{spec} = '1-2d3';
            $Opt{verbose} = 2;
            $Opt{loglevel} = 3;
            $Opt{calendar} = 1;
            my $now = stamp2time('20111027-224500');
            %S = (
                # days with 0,1,2,3 snapshots 
                mks('20111026-093000'), 
                mks('20111025-093000'), mks('20111025-103000'), 
                mks('20111024-093000'), mks('20111024-103000'), mks('20111024-113000'),  
            )
            ;
            my $dest = ${$Opt{destination}}[0];
            my ($warnings, $errors, @specs) = parseSpecs($Opt{spec}); 
            my @cSpecs = compileSpecs($now, \@specs);
            displayCSpecs(@cSpecs);
        }

        when (18) {
            print "Testing prevPeriod with difficult dates\n";
            # 0=h 1=d 2=w 3=m 4=y
            my ($beg, $end);
            sub x {
                my $now = shift;
                my $periodn = shift;
                my ($beg, $end) = prevPeriod(stamp2time($now), $periodn);
                print "old: One $LevelName[$periodn] before $now was " . time2stampz($beg) . "\n";
                ($beg, $end) = prevPeriod2(stamp2time($now), $periodn);
                print "new: One $LevelName[$periodn] before $now was " . time2stampz($beg) . "\n";
            }
#            $Opt{calendar} = 0;
#            print "Non-calendar mode: \n";
            print "* jump into missing March hour\n";
            x('20120325-013000', 0);
            x('20120325-023000', 0);
            x('20120325-033000', 0);
            x('20120324-013000', 1);
            x('20120325-013000', 1);
            x('20120326-013000', 1);
            x('20120401-013000', 2);
            x('20120425-013000', 3);
            x('20120425-023000', 3);
            print "* jump from extra October hour\n";
            x('20121028-013000', 0);    # seems to get nowhere if tz ignored!
            x('20121028-023000', 0);
            x('20121028-033000', 0);
            print "* jump into missing 29 Feb\n";
            x('20110329-120000', 3);
#            $Opt{calendar} = 1;
#            print "Calendar mode: \n";
#            x('20120325-013000', 0);
#            x('20120325-023000', 0);
#            x('20120325-033000', 0);
#            x('20120324-013000', 1);
#            x('20120325-013000', 1);
#            x('20120326-013000', 1);
#            x('20120401-013000', 2);
        }

        when (19) {
            print "Testing bytes()\n";
            my $mult = 1;
            for my $count (1..5) {
                foreach my $b (1, 1.234, 123, 123.456, 999, 999.5678, 1000, 1000.1111,
                        1023, 1023.999, 1024, 1024.1024) {
                    print $b*$mult . " -- " . bytes($b*$mult) . "\n";
                }
                $mult *= 1024;
            }
        }

        when (20) {
            print "Testing gMS with partial periods\n";
            $Opt{debug} = {'gdl'=>1, 'cs'=>1};
            $Opt{verbose} = 3;
            my (undef, undef, @specs) = parseSpecs('1d,3w1');
            %S = (
                mks('20121001-120000'), mks('20121002-120000'), 
                mks('20121004-120000'), 
                mks('20121005-120000'), mks('20121006-120000'), mks('20121007-120000'),  

                mks('20121008-120000'), mks('20121009-120000'), mks('20121010-120000'),  
            );
            my $now = stamp2time('20121010-181500');
            my @cSpecs = compileSpecs($now, \@specs);
            displayCSpecs(@cSpecs);
            my @D = getDeleteList(@cSpecs);
        }

        when (21) {
            print "Testing nS fudgetime\n";
            $Opt{debug} = {'ns'=>1};
            $Opt{verbose} = 3;
            $Opt{calendar} = 0;
            my ($w, $e, $msg, @pspecs) = processSpecs('1h,1d,3w1');
            $Opt{pspecs} = \@pspecs if !$e;
#            %S = (
#                mks('20121010-180005'), 
#            );
            # ! have to hack nS() to override use of time()
            # so this test is pretty useless
            my $newS = newSnapshot('nowhere', stamp2time('20121010-180005'));
        }


    }
}

#===================================================================================================

# Mainline is here

# Display basic info -- logging will happen later
my $intro = "$Script version $VERSION starting at " . (strftime "%H:%M:%S", localtime), 1;
printit under2("\n$intro\n"), 1;
printit "Command line: $0 @ARGV\n", 3;

# No options supplied -- display help and exit
if (@ARGV == 0) {
    printit "ERROR: No options given\n"; 
    showHelp;
    exit EXITCONFIG;
}

# Get and validate options
if (processOptions() > 0) {
    printlog "ERROR: Problems found in configuration -- stopping\n", 1;
    printlog "Try '$Script --help' or 'man $Script' for help.\n", 1;
    logit '-'x80 . "\n", 1;
    exit EXITCONFIG;
}

# Help requested - display and exit
if ($Opt{help}) {
    showHelp;
    exit EXITOK;
}

# Version requested - display and exit
if ($Opt{version}) {
    showLicence;
    exit EXITOK;
}

# Turn off stacktrace unless we're debugging
if (keys $Opt{debug} == 0) {
    $SIG{__DIE__} = 'DEFAULT';  
} 

# Just run the test suite if required 
if ($Opt{test}) {
    testSuite();
    exit;
}

displayOptions();

# Deal with each destination
foreach my $dest (@{$Opt{destination}}) {
    last if $Interrupted;
    debug "running destination $dest->{location}\n", 1;
    local %Opt = %Opt;
    foreach my $destOpt (keys %{$dest->{options}}) {
        debug "overriding $destOpt with '$dest->{options}->{$destOpt}'\n", 1;
        $Opt{$destOpt} = $dest->{options}->{$destOpt};
    }
    if ($Opt{status}) {
        getSnapshotList($dest->{location});
        my @cSpecs = compileSpecs(time(), $Opt{pspecs});
        displayCSpecs(@cSpecs) if $Opt{debug}->{status};
        status($dest->{location}, @cSpecs);
    } else {
        processDestination($dest->{location});
    }
}

displaySummary();

if ($Interrupted) { # FIXME is this useful?
    printlog "\n(Interrupted with signal '$Interrupted')\n", 1;
    $ExitCode = EXITINT if $ExitCode != EXITOK;
}
printlog "\n$Script stopping at " . (strftime "%H:%M:%S", localtime) . 
    " with exit code $ExitCode\n", 1,1;
logit '-' x 80, 1;

# Free the lock on the logfile
close(LOGFILE);    # closing the file automatically unlocks it

exit $ExitCode;
# That's the end

#===================================================================================================

# vim: tabstop=4 softtabstop=4 shiftwidth=4 expandtab
